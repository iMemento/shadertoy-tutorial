---
sidebar_position: 1
---

# 8.1 æ—‹è½¬

è®©æˆ‘ä»¬åˆ›å»ºä¸€ä¸ªæ–°çš„ç€è‰²å™¨ï¼Œå¹¶ä½¿ç”¨æ­¤ Shadertoy ç³»åˆ—çš„ç¬¬ 7 éƒ¨åˆ†æœ«å°¾çš„ä»£ç ã€‚ä½†æ˜¯ï¼Œæˆ‘ä»¬å°†åˆ é™¤çƒä½“ã€‚

```cpp
const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float PRECISION = 0.001;

struct Surface {
    float sd; // signed distance value
    vec3 col; // color
};

Surface sdFloor(vec3 p, vec3 col) {
  float d = p.y + 1.;
  return Surface(d, col);
}

Surface minWithColor(Surface obj1, Surface obj2) {
  if (obj2.sd < obj1.sd) return obj2;
  return obj1;
}

Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0));
  Surface co = sdFloor(p, floorColor);
  return co;
}

Surface rayMarch(vec3 ro, vec3 rd, float start, float end) {
  float depth = start;
  Surface co; // closest object

  for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
    vec3 p = ro + depth * rd;
    co = sdScene(p);
    depth += co.sd;
    if (co.sd < PRECISION || depth > end) break;
  }

  co.sd = depth;

  return co;
}

vec3 calcNormal(in vec3 p) {
    vec2 e = vec2(1.0, -1.0) * 0.0005; // epsilon
    return normalize(
      e.xyy * sdScene(p + e.xyy).sd +
      e.yyx * sdScene(p + e.yyx).sd +
      e.yxy * sdScene(p + e.yxy).sd +
      e.xxx * sdScene(p + e.xxx).sd);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
  vec3 backgroundColor = vec3(0.835, 1, 1);

  vec3 col = vec3(0);
  vec3 ro = vec3(0, 0, 3); // ray origin that represents camera position
  vec3 rd = normalize(vec3(uv, -1)); // ray direction

  Surface co = rayMarch(ro, rd, MIN_DIST, MAX_DIST); // closest object

  if (co.sd > MAX_DIST) {
    col = backgroundColor; // ray didn't hit anything
  } else {
    vec3 p = ro + rd * co.sd; // point on cube or floor we discovered from ray marching
    vec3 normal = calcNormal(p);
    vec3 lightPosition = vec3(2, 2, 7);
    vec3 lightDirection = normalize(lightPosition - p);

    float dif = clamp(dot(normal, lightDirection), 0.3, 1.); // diffuse reflection

    col = dif * co.col + backgroundColor * .2; // Add a bit of background color to the diffuse color
  }

  // Output to screen
  fragColor = vec4(col, 1.0);
}
```

è¿è¡Œä»£ç åï¼Œæ‚¨åº”è¯¥ä¼šçœ‹åˆ°å¹³é“ºåœ°æ¿å’Œæµ…è“è‰²èƒŒæ™¯è‰²ã€‚
<p align="center">![-](./img/img-1.png)</p>

## æ·»åŠ ä¸€ä¸ªcube
æ¥ä¸‹æ¥ï¼Œæˆ‘ä»¬å°†åˆ©ç”¨ `Inigo Quilez` ç½‘ç«™ä¸Šçš„ [**3D SDF**](https://www.iquilezles.org/www/articles/distfunctions/distfunctions.htm) åˆ—è¡¨æ¥æ·»åŠ ä¸€ä¸ªç«‹æ–¹ä½“ã€‚åœ¨`Primitivesï¼ˆå›¾å…ƒï¼‰`éƒ¨åˆ†ä¸‹ï¼Œæ‚¨å°†æ‰¾åˆ°ä¸€ä¸ªæ ‡æœ‰`Box - exact`çš„ SDFï¼Œæˆ‘ä»¬å°†ä½¿ç”¨å®ƒæ¥æ¸²æŸ“ç«‹æ–¹ä½“ã€‚

```cpp
float sdBox( vec3 p, vec3 b )
{
  vec3 q = abs(p) - b;
  return length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
}
```

ä¸ºäº†ä½¿å…¶ä¸æˆ‘ä»¬åœ¨ä¸Šä¸€ä¸ªæ•™ç¨‹ä¸­å­¦åˆ°çš„ä»£ç å…¼å®¹ï¼Œå¹¶ä¸ºå¯¹è±¡æ·»åŠ å”¯ä¸€é¢œè‰²ï¼Œæˆ‘ä»¬éœ€è¦è¿”å› `Surface` ç±»å‹çš„å€¼ï¼Œè€Œä¸æ˜¯æµ®ç‚¹æ•°ã€‚æˆ‘ä»¬è¿˜å°†å‘å‡½æ•°æ·»åŠ ä¸¤ä¸ªå‚æ•°ï¼š`offset` å’Œ `color`ã€‚

```cpp
Surface sdBox( vec3 p, vec3 b, vec3 offset, vec3 col)
{
  p = p - offset;
  vec3 q = abs(p) - b;
  float d = length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
  return Surface(d, col);
}
```

ç¬¬ä¸€ä¸ªå‚æ•° `p` æ˜¯é‡‡æ ·ç‚¹ï¼Œç¬¬äºŒä¸ªå‚æ•° `b` æ˜¯è¡¨ç¤ºæ¡†è¾¹ç•Œçš„ `vec3` å˜é‡ã€‚ä½¿ç”¨ `x`ã€`y` å’Œ `z`åˆ†é‡æ§åˆ¶é•¿æ–¹ä½“çš„å®½åº¦ã€é«˜åº¦å’Œæ·±åº¦ã€‚å¦‚æœæˆ‘ä»¬ä½¿è¿™ä¸‰ä¸ªå€¼éƒ½ç›¸åŒï¼Œé‚£ä¹ˆæˆ‘ä»¬æœ€ç»ˆä¼šå¾—åˆ°ä¸€ä¸ªç«‹æ–¹ä½“ã€‚

è®©æˆ‘ä»¬åœ¨ 3D åœºæ™¯ä¸­æ’å…¥ä¸€ä¸ªç«‹æ–¹ä½“ï¼š

```cpp
Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0));
  Surface co = sdFloor(p, floorColor);
  co = minWithColor(co, sdBox(p, vec3(1), vec3(0, 0.5, -4), vec3(1, 0, 0)));
  return co;
}
```

æ­¤ç«‹æ–¹ä½“çš„å°ºå¯¸ä¸º `1x1x1`ï¼Œä½ç½®ä¸º `(0, 0.5, -4)`ï¼Œé¢œè‰²ä¸ºçº¢è‰²ã€‚
<p align="center">![-](./img/img-2.png)</p>

## æ—‹è½¬çŸ©é˜µ

åœ¨çº¿æ€§ä»£æ•°ä¸­ï¼Œ[**å˜æ¢çŸ©é˜µ**](https://en.wikipedia.org/wiki/Transformation_matrix)ç”¨äºå¯¹ `2D` å’Œ `3D` å½¢çŠ¶æ‰§è¡Œå„ç§æ“ä½œï¼šæ‹‰ä¼¸ã€æŒ¤å‹ã€æ—‹è½¬ã€å‰ªåˆ‡å’Œåå°„ã€‚æ¯ä¸ªçŸ©é˜µè¡¨ç¤ºä¸€ä¸ªæ“ä½œã€‚

é€šè¿‡å°†å›¾å½¢ä¸Šçš„ç‚¹ï¼ˆæˆ– `GLSL` ä»£ç ä¸­çš„æ ·æœ¬ç‚¹ï¼‰ä¹˜ä»¥è½¬æ¢çŸ©é˜µï¼Œæˆ‘ä»¬å¯ä»¥æ‰§è¡Œè¿™äº›æ“ä½œä¸­çš„ä»»ä½•ä¸€ä¸ªã€‚æˆ‘ä»¬è¿˜å¯ä»¥å°†è¿™äº›å˜æ¢çŸ©é˜µä¸­çš„ä»»ä½•ä¸€ä¸ªç›¸ä¹˜ï¼Œä»¥åˆ›å»ºæ‰§è¡Œå¤šä¸ªè¿ç®—çš„æ–°å˜æ¢çŸ©é˜µã€‚

ç”±äº**çŸ©é˜µä¹˜æ³•æ˜¯ä¸æ»¡è¶³äº¤æ¢å¾‹**çš„ï¼Œå› æ­¤æˆ‘ä»¬å°†çŸ©é˜µç›¸ä¹˜çš„é¡ºåºå¾ˆé‡è¦ã€‚å¦‚æœæ—‹è½¬å½¢çŠ¶ï¼Œç„¶ååˆ‡å˜ï¼Œåˆ™æœ€ç»ˆå¾—åˆ°çš„ç»“æœä¸å…ˆåˆ‡å˜åæ—‹è½¬çš„ç»“æœä¸åŒã€‚åŒæ ·ï¼Œå¦‚æœå…ˆæ²¿ `x` è½´æ—‹è½¬å½¢çŠ¶ï¼Œç„¶åå†æ²¿ `z` è½´æ—‹è½¬å½¢çŠ¶ï¼Œåˆ™å¦‚æœé¢ å€’è¿™äº›ä½œçš„é¡ºåºï¼Œåˆ™æœ€ç»ˆå¯èƒ½ä¼šå¾—åˆ°ä¸åŒçš„ç»“æœã€‚

æ—‹è½¬çŸ©é˜µæ˜¯ä¸€ç§å˜æ¢çŸ©é˜µã€‚æˆ‘ä»¬æ¥çœ‹çœ‹æœ¬æ•™ç¨‹ä¸­å°†ä½¿ç”¨çš„[**æ—‹è½¬çŸ©é˜µ**](https://en.wikipedia.org/wiki/Rotation_matrix#Basic_rotations)ã€‚

<p align="center">![-](./img/img-3.png)</p>
<p align="center">Rotation Matrices æ¥è‡ª [ç»´åŸºç™¾ç§‘](https://en.wikipedia.org/wiki/Rotation_matrix#Basic_rotations)ä¸Šçš„æ•°æ®</p>

åœ¨ä¸Šå›¾ä¸­ï¼Œæˆ‘ä»¬æœ‰ä¸‰ä¸ªæ—‹è½¬çŸ©é˜µï¼Œæ¯ä¸ªæ—‹è½¬çŸ©é˜µå¯¹åº” 3D ä¸­çš„æ¯ä¸ªè½´ã€‚è¿™äº›å°†å…è®¸æˆ‘ä»¬å›´ç»•è½´æ—‹è½¬å½¢çŠ¶ï¼Œå°±å¥½åƒä½“æ“è¿åŠ¨å‘˜ç»•ç€æ é“ƒæˆ–æ†å­æ‘†åŠ¨ä¸€æ ·ã€‚

åœ¨ä»£ç çš„é¡¶éƒ¨ï¼Œè®©æˆ‘ä»¬æ·»åŠ è·¨æ¯ä¸ªè½´çš„æ—‹è½¬çŸ©é˜µçš„å‡½æ•°ã€‚æˆ‘ä»¬è¿˜å°†æ·»åŠ ä¸€ä¸ªè¿”å›[**å•ä½çŸ©é˜µ**](https://en.wikipedia.org/wiki/Identity_matrix)çš„å‡½æ•°ï¼Œä»¥ä¾¿æˆ‘ä»¬å¯ä»¥é€‰æ‹©ä¸æ‰§è¡Œä»»ä½•ç±»å‹çš„è½¬æ¢ã€‚

```cpp
// Rotation matrix around the X axis.
mat3 rotateX(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(1, 0, 0),
        vec3(0, c, -s),
        vec3(0, s, c)
    );
}

// Rotation matrix around the Y axis.
mat3 rotateY(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, 0, s),
        vec3(0, 1, 0),
        vec3(-s, 0, c)
    );
}

// Rotation matrix around the Z axis.
mat3 rotateZ(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, -s, 0),
        vec3(s, c, 0),
        vec3(0, 0, 1)
    );
}

// Identity matrix.
mat3 identity() {
    return mat3(
        vec3(1, 0, 0),
        vec3(0, 1, 0),
        vec3(0, 0, 1)
    );
}
```

æˆ‘ä»¬ç°åœ¨éœ€è¦è°ƒæ•´ `sdBox` å‡½æ•°ä»¥æ¥å—çŸ©é˜µå˜æ¢ä½œä¸ºå¦ä¸€ä¸ªå‚æ•°ã€‚æˆ‘ä»¬å°†é‡‡æ ·ç‚¹ä¹˜ä»¥æ—‹è½¬çŸ©é˜µã€‚åœ¨å°†é‡‡æ ·ç‚¹ç§»åŠ¨åˆ°ç”±åç§»é‡å®šä¹‰çš„æŸä¸ªä¸–ç•Œåæ ‡åï¼Œå°†åº”ç”¨æ­¤å˜æ¢ã€‚

```cpp
Surface sdBox( vec3 p, vec3 b, vec3 offset, vec3 col, mat3 transform)
{
  p = (p - offset) * transform;
  vec3 q = abs(p) - b;
  float d = length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
  return Surface(d, col);
}
```

ç„¶åæˆ‘ä»¬éœ€è¦ä¿®æ”¹ `sdScene` å‡½æ•°ï¼Œåœ¨å¯¹ `sdBox` å‡½æ•°çš„è°ƒç”¨ä¸­æ’å…¥ä¸€ä¸ªæ–°å‚æ•°ï¼š
```cpp
Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0));
  Surface co = sdFloor(p, floorColor);
  co = minWithColor(co, sdBox(p, vec3(1), vec3(0, 0.5, -4), vec3(1, 0, 0), rotateX(iTime)));
  return co;
}
```
æˆ‘ä»¬å¯ä»¥åœ¨ `rotateX`ã€`rotateY` å’Œ `rotateZ` ä¹‹é—´åˆ†åˆ«åœ¨ `x` è½´ã€`y` è½´å’Œ `z`è½´ä¸Šæ—‹è½¬ç«‹æ–¹ä½“ã€‚è§’åº¦å°†è®¾ç½®ä¸º `iTime`ï¼Œå› æ­¤æˆ‘ä»¬å¯ä»¥çœ‹åˆ°ç«‹æ–¹ä½“æ—‹è½¬éšæ—¶é—´çš„åŠ¨ç”»æ•ˆæœã€‚ç«‹æ–¹ä½“çš„æ¢è½´ç‚¹å°†æ˜¯å®ƒè‡ªå·±çš„ä¸­å¿ƒã€‚

ä¸‹é¢æ˜¯åœ¨è°ƒç”¨ `sdBox` å‡½æ•°æ—¶ä½¿ç”¨ `rotateX (iTime)` æ²¿ `x`è½´æ—‹è½¬ç«‹æ–¹ä½“çš„ç¤ºä¾‹ã€‚
<p align="center">![-](./img/gif-1.gif)</p>

ä¸‹é¢æ˜¯åœ¨è°ƒç”¨ `sdBox` å‡½æ•°æ—¶ä½¿ç”¨ `rotateYï¼ˆiTimeï¼‰` æ²¿ `y` è½´æ—‹è½¬ç«‹æ–¹ä½“çš„ç¤ºä¾‹ã€‚
<p align="center">![-](./img/gif-2.gif)</p>

ä¸‹é¢æ˜¯åœ¨è°ƒç”¨ `sdBox` å‡½æ•°æ—¶ä½¿ç”¨ `rotateZï¼ˆiTimeï¼‰` æ²¿ `z` è½´æ—‹è½¬ç«‹æ–¹ä½“çš„ç¤ºä¾‹ã€‚
<p align="center">![-](./img/gif-3.gif)</p>

ä¸ºäº†é˜²æ­¢ä»»ä½•ç±»å‹çš„æ—‹è½¬ï¼Œæˆ‘ä»¬å¯ä»¥è°ƒç”¨ `identity` å‡½æ•°ï¼š
```cpp
Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0));
  Surface co = sdFloor(p, floorColor);
  co = minWithColor(co, sdBox(p, vec3(1), vec3(0, 0.5, -4), vec3(1, 0, 0), identity())); // By using the identity matrix, the cube's orientation remains the same
  return co;
}
```

æ‚¨è¿˜å¯ä»¥é€šè¿‡å°†å„ä¸ªçŸ©é˜µè½¬æ¢ç›¸ä¹˜æ¥ç»„åˆå®ƒä»¬ã€‚è¿™å°†å¯¼è‡´ç«‹æ–¹ä½“åŒæ—¶åœ¨æ‰€æœ‰è½´ä¸Šæ—‹è½¬ã€‚

```cpp
Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0));
  Surface co = sdFloor(p, floorColor);
  co = minWithColor(co, sdBox(
      p,
      vec3(1),
      vec3(0, 0.5, -4),
      vec3(1, 0, 0),
      rotateX(iTime) * rotateY(iTime) * rotateZ(iTime) // Combine rotation matrices
  ));
  return co;
}
```
<p align="center">![-](./img/gif-4.gif)</p>

æ‚¨å¯ä»¥åœ¨ä¸‹é¢æ‰¾åˆ°å·²å®Œæˆä»£ç çš„ç¤ºä¾‹ï¼š
```cpp
// Rotation matrix around the X axis.
mat3 rotateX(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(1, 0, 0),
        vec3(0, c, -s),
        vec3(0, s, c)
    );
}

// Rotation matrix around the Y axis.
mat3 rotateY(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, 0, s),
        vec3(0, 1, 0),
        vec3(-s, 0, c)
    );
}

// Rotation matrix around the Z axis.
mat3 rotateZ(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, -s, 0),
        vec3(s, c, 0),
        vec3(0, 0, 1)
    );
}

// Identity matrix.
mat3 identity() {
    return mat3(
        vec3(1, 0, 0),
        vec3(0, 1, 0),
        vec3(0, 0, 1)
    );
}

const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float PRECISION = 0.001;

struct Surface {
    float sd; // signed distance value
    vec3 col; // color
};

Surface sdBox( vec3 p, vec3 b, vec3 offset, vec3 col, mat3 transform)
{
  p = (p - offset) * transform;
  vec3 q = abs(p) - b;
  float d = length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
  return Surface(d, col);
}

Surface sdFloor(vec3 p, vec3 col) {
  float d = p.y + 1.;
  return Surface(d, col);
}

Surface minWithColor(Surface obj1, Surface obj2) {
  if (obj2.sd < obj1.sd) return obj2;
  return obj1;
}

Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0));
  Surface co = sdFloor(p, floorColor);
  co = minWithColor(co, sdBox(
      p,
      vec3(1),
      vec3(0, 0.5, -4),
      vec3(1, 0, 0),
      rotateX(iTime)*rotateY(iTime)*rotateZ(iTime) // Combine rotation matrices
  ));
  return co;
}

Surface rayMarch(vec3 ro, vec3 rd, float start, float end) {
  float depth = start;
  Surface co; // closest object

  for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
    vec3 p = ro + depth * rd;
    co = sdScene(p);
    depth += co.sd;
    if (co.sd < PRECISION || depth > end) break;
  }

  co.sd = depth;

  return co;
}

vec3 calcNormal(in vec3 p) {
    vec2 e = vec2(1.0, -1.0) * 0.0005; // epsilon
    return normalize(
      e.xyy * sdScene(p + e.xyy).sd +
      e.yyx * sdScene(p + e.yyx).sd +
      e.yxy * sdScene(p + e.yxy).sd +
      e.xxx * sdScene(p + e.xxx).sd);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
  vec3 backgroundColor = vec3(0.835, 1, 1);

  vec3 col = vec3(0);
  vec3 ro = vec3(0, 0, 3); // ray origin that represents camera position
  vec3 rd = normalize(vec3(uv, -1)); // ray direction

  Surface co = rayMarch(ro, rd, MIN_DIST, MAX_DIST); // closest object

  if (co.sd > MAX_DIST) {
    col = backgroundColor; // ray didn't hit anything
  } else {
    vec3 p = ro + rd * co.sd; // point on cube or floor we discovered from ray marching
    vec3 normal = calcNormal(p);
    vec3 lightPosition = vec3(2, 2, 7);
    vec3 lightDirection = normalize(lightPosition - p);

    float dif = clamp(dot(normal, lightDirection), 0.3, 1.); // diffuse reflection

    col = dif * co.col + backgroundColor * .2; // Add a bit of background color to the diffuse color
  }

  // Output to screen
  fragColor = vec4(col, 1.0);
}
```

## å›´ç»•æ¢è½´ç‚¹æ—‹è½¬
å¦‚æœæˆ‘ä»¬æƒ³è®©ç«‹æ–¹ä½“çœ‹èµ·æ¥åƒæ˜¯å›´ç»•ä¸€ä¸ªä¸æ˜¯ç«‹æ–¹ä½“ä¸­å¿ƒçš„å¤–éƒ¨æ¢è½´ç‚¹æ—‹è½¬ï¼Œé‚£ä¹ˆæˆ‘ä»¬å¿…é¡»ä¿®æ”¹ `sdBox` å‡½æ•°ï¼Œä»¥ä¾¿åœ¨è½¬æ¢åå°†ç«‹æ–¹ä½“ç§»åŠ¨ä¸€å®šè·ç¦»ã€‚
```cpp
Surface sdBox( vec3 p, vec3 b, vec3 offset, vec3 col, mat3 transform)
{
  p = (p - offset) * transform - vec3(3, 0, 0); // Move the cube as it is rotating
  vec3 q = abs(p) - b;
  float d = length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
  return Surface(d, col);
}
```

å¦‚æœæˆ‘ä»¬åœ¨ `sdScene` å‡½æ•°ä¸­ä½¿ç”¨ `rotateYï¼ˆiTimeï¼‰`ï¼Œç«‹æ–¹ä½“ä¼¼ä¹ä¼šæ²¿ç€è·ç¦»ç«‹æ–¹ä½“ä¸€å®šè·ç¦»çš„æ¢è½´ç‚¹ç»• `y` è½´æ—‹è½¬ã€‚åœ¨è¿™ä¸ªä¾‹å­ä¸­ï¼Œæˆ‘ä»¬ä½¿ç”¨ `vec3ï¼ˆ3ï¼Œ 0ï¼Œ 0ï¼‰` è®©ç«‹æ–¹ä½“ä¿æŒ `3` ä¸ªå•ä½çš„è·ç¦»ï¼ŒåŒæ—¶å®ƒå›´ç»•ä½äº `ï¼ˆ0ï¼Œ 0.5ï¼Œ -4ï¼‰` çš„æ¢è½´ç‚¹æ—‹è½¬ï¼Œè¿™æ˜¯æˆ‘ä»¬åœ¨ `sdScene` å‡½æ•°ä¸­åˆ†é…ç»™ `sdBox` çš„åç§»é‡ã€‚
<p align="center">![-](./img/gif-5.gif)</p>
ä»¥ä¸‹æ˜¯ç”¨äºåˆ›å»ºä¸Šè¿°å›¾åƒçš„å®Œæ•´ä»£ç ï¼š

```cpp
// Rotation matrix around the X axis.
mat3 rotateX(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(1, 0, 0),
        vec3(0, c, -s),
        vec3(0, s, c)
    );
}

// Rotation matrix around the Y axis.
mat3 rotateY(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, 0, s),
        vec3(0, 1, 0),
        vec3(-s, 0, c)
    );
}

// Rotation matrix around the Z axis.
mat3 rotateZ(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, -s, 0),
        vec3(s, c, 0),
        vec3(0, 0, 1)
    );
}

// Identity matrix.
mat3 identity() {
    return mat3(
        vec3(1, 0, 0),
        vec3(0, 1, 0),
        vec3(0, 0, 1)
    );
}

const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float PRECISION = 0.001;

struct Surface {
    float sd; // signed distance value
    vec3 col; // color
};

Surface sdBox( vec3 p, vec3 b, vec3 offset, vec3 col, mat3 transform)
{
  p = (p - offset) * transform - vec3(3, 0, 0); // Move the cube as it is rotating
  vec3 q = abs(p) - b;
  float d = length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
  return Surface(d, col);
}

Surface sdFloor(vec3 p, vec3 col) {
  float d = p.y + 1.;
  return Surface(d, col);
}

Surface minWithColor(Surface obj1, Surface obj2) {
  if (obj2.sd < obj1.sd) return obj2;
  return obj1;
}

Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0));
  Surface co = sdFloor(p, floorColor);
  co = minWithColor(co, sdBox(p, vec3(1), vec3(0, 0.5, -4), vec3(1, 0, 0), rotateY(iTime)));
  return co;
}

Surface rayMarch(vec3 ro, vec3 rd, float start, float end) {
  float depth = start;
  Surface co; // closest object

  for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
    vec3 p = ro + depth * rd;
    co = sdScene(p);
    depth += co.sd;
    if (co.sd < PRECISION || depth > end) break;
  }

  co.sd = depth;

  return co;
}

vec3 calcNormal(in vec3 p) {
    vec2 e = vec2(1.0, -1.0) * 0.0005; // epsilon
    return normalize(
      e.xyy * sdScene(p + e.xyy).sd +
      e.yyx * sdScene(p + e.yyx).sd +
      e.yxy * sdScene(p + e.yxy).sd +
      e.xxx * sdScene(p + e.xxx).sd);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
  vec3 backgroundColor = vec3(0.835, 1, 1);

  vec3 col = vec3(0);
  vec3 ro = vec3(0, 0, 3); // ray origin that represents camera position
  vec3 rd = normalize(vec3(uv, -1)); // ray direction

  Surface co = rayMarch(ro, rd, MIN_DIST, MAX_DIST); // closest object

  if (co.sd > MAX_DIST) {
    col = backgroundColor; // ray didn't hit anything
  } else {
    vec3 p = ro + rd * co.sd; // point on cube or floor we discovered from ray marching
    vec3 normal = calcNormal(p);
    vec3 lightPosition = vec3(2, 2, 7);
    vec3 lightDirection = normalize(lightPosition - p);

    float dif = clamp(dot(normal, lightDirection), 0.3, 1.); // diffuse reflection

    col = dif * co.col + backgroundColor * .2; // Add a bit of background color to the diffuse color
  }

  // Output to screen
  fragColor = vec4(col, 1.0);
}
```

## ç»“è®º
åœ¨æœ¬æ•™ç¨‹ä¸­ï¼Œæˆ‘ä»¬å­¦ä¹ äº†å¦‚ä½•åœ¨ 3D ç©ºé—´ä¸­çš„æ¯ä¸ªè½´ä¸Šæ—‹è½¬ç«‹æ–¹ä½“ã€‚æˆ‘ä»¬è¿˜å­¦ä¹ äº†å¦‚ä½•å›´ç»•å¤–éƒ¨æ¢è½´ç‚¹æ—‹è½¬ç«‹æ–¹ä½“ï¼Œä½¿å…¶çœ‹èµ·æ¥åƒæ˜¯å›´ç»•ç©ºé—´ä¸­çš„æŸä¸ªç‚¹æ—‹è½¬ã€‚æ‚¨ä»Šå¤©å­¦åˆ°çš„çŸ¥è¯†ä¹Ÿé€‚ç”¨äºæ‰€æœ‰å…¶ä»– 3D å¯¹è±¡ã€‚æˆ‘ä»¬é€‰æ‹©äº†ç«‹æ–¹ä½“è€Œä¸æ˜¯çƒä½“ï¼Œå› ä¸ºå®ƒæ›´å®¹æ˜“æ£€æŸ¥æˆ‘ä»¬çš„æ—‹è½¬çŸ©é˜µæ˜¯å¦ğŸ™‚èµ·ä½œç”¨ã€‚




