---
sidebar_position: 2
---

# 11.2 å¤šä¸ªå…‰æº

æ‚¨å¯èƒ½å·²ç»æ³¨æ„åˆ°ï¼Œ`Phong` åå°„æ–¹ç¨‹å¯¹æ¼«åå°„å’Œé•œé¢åå°„åˆ†é‡ä½¿ç”¨æ±‚å’Œã€‚å¦‚æœå‘åœºæ™¯ä¸­æ·»åŠ æ›´å¤šç¯å…‰ï¼Œåˆ™æ¯ä¸ªç¯å…‰éƒ½å°†å…·æœ‰æ¼«åå°„å’Œé•œé¢åå°„ç»„ä»¶ã€‚

<p align="center">![-](./img/img-2.png)</p>


## å¤šä¸ªå…‰æº
ä¸ºäº†æ›´è½»æ¾åœ°å¤„ç†å¤šä¸ªå…‰æºï¼Œæˆ‘ä»¬å°†åˆ›å»ºä¸€ä¸ª `phong` å‡½æ•°ã€‚ç”±äºè¿™ä¸ªåœºæ™¯åªä¸ºä¸€ä¸ªå¯¹è±¡ç€è‰²ï¼Œæˆ‘ä»¬ä¹Ÿå¯ä»¥åœ¨ `phong` å‡½æ•°ä¸­æ”¾ç½®åå°„ç³»æ•°`ï¼ˆk_aã€k_dã€k_sï¼‰`å’Œå¼ºåº¦ã€‚

```cpp
const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float PRECISION = 0.001;

float sdSphere(vec3 p, float r )
{
  return length(p) - r;
}

float sdScene(vec3 p) {
  return sdSphere(p, 1.);
}

float rayMarch(vec3 ro, vec3 rd) {
  float depth = MIN_DIST;

  for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
    vec3 p = ro + depth * rd;
    float d = sdScene(p);
    depth += d;
    if (d < PRECISION || depth > MAX_DIST) break;
  }

  return depth;
}

vec3 calcNormal(vec3 p) {
    vec2 e = vec2(1.0, -1.0) * 0.0005;
    return normalize(
      e.xyy * sdScene(p + e.xyy) +
      e.yyx * sdScene(p + e.yyx) +
      e.yxy * sdScene(p + e.yxy) +
      e.xxx * sdScene(p + e.xxx));
}

mat3 camera(vec3 cameraPos, vec3 lookAtPoint) {
    vec3 cd = normalize(lookAtPoint - cameraPos); // camera direction
    vec3 cr = normalize(cross(vec3(0, 1, 0), cd)); // camera right
    vec3 cu = normalize(cross(cd, cr)); // camera up

    return mat3(-cr, cu, -cd);
}

vec3 phong(vec3 lightDir, vec3 normal, vec3 rd) {
  // ambient
  float k_a = 0.6;
  vec3 i_a = vec3(0.7, 0.7, 0);
  vec3 ambient = k_a * i_a;

  // diffuse
  float k_d = 0.5;
  float dotLN = clamp(dot(lightDir, normal), 0., 1.);
  vec3 i_d = vec3(0.7, 0.5, 0);
  vec3 diffuse = k_d * dotLN * i_d;

  // specular
  float k_s = 0.6;
  float dotRV = clamp(dot(reflect(lightDir, normal), -rd), 0., 1.);
  vec3 i_s = vec3(1, 1, 1);
  float alpha = 10.;
  vec3 specular = k_s * pow(dotRV, alpha) * i_s;

  return ambient + diffuse + specular;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
  vec3 backgroundColor = vec3(0.835, 1, 1);
  vec3 col = vec3(0);

  vec3 lp = vec3(0); // lookat point (aka camera target)
  vec3 ro = vec3(0, 0, 3);

  vec3 rd = camera(ro, lp) * normalize(vec3(uv, -1)); // ray direction

  float d = rayMarch(ro, rd);

  if (d > MAX_DIST) {
    col = backgroundColor;
  } else {
      vec3 p = ro + rd * d; // point on surface found by ray marching
      vec3 normal = calcNormal(p); // surface normal

      // light #1
      vec3 lightPosition1 = vec3(-8, -6, -5);
      vec3 lightDirection1 = normalize(lightPosition1 - p);
      float lightIntensity1 = 0.6;

      // light #2
      vec3 lightPosition2 = vec3(1, 1, 1);
      vec3 lightDirection2 = normalize(lightPosition2 - p);
      float lightIntensity2 = 0.7;

      // final sphere color
      col = lightIntensity1 * phong(lightDirection1, normal, rd);
      col += lightIntensity2 * phong(lightDirection2, normal , rd);
  }

  fragColor = vec4(col, 1.0);
}
```

æˆ‘ä»¬å¯ä»¥å°† `phong` å‡½æ•°çš„ç»“æœä¹˜ä»¥å…‰å¼ºåº¦å€¼ï¼Œè¿™æ ·çƒä½“å°±ä¸ä¼šæ˜¾å¾—å¤ªäº®ã€‚å½“æ‚¨è¿è¡Œä»£ç æ—¶ï¼Œæ‚¨çš„çƒä½“åº”è¯¥çœ‹èµ·æ¥æ›´é—ªäº®!!

<p align="center">![-](./img/img-9.png)</p>

## ä¸ºå¤šä¸ªå¯¹è±¡ç€è‰²
å°†æ‰€æœ‰åå°„ç³»æ•°å’Œå¼ºåº¦éƒ½æ”¾åœ¨ `phong` å‡½æ•°ä¸­ä¸æ˜¯å¾ˆå®ç”¨ã€‚åœºæ™¯ä¸­å¯ä»¥æœ‰å¤šä¸ªå¯¹è±¡ï¼Œè¿™äº›å¯¹è±¡å…·æœ‰ä¸åŒçš„æè´¨ç±»å‹ã€‚æŸäº›å¯¹è±¡å¯èƒ½çœ‹èµ·æ¥æœ‰å…‰æ³½å’Œåå°„ï¼Œè€Œå…¶ä»–å¯¹è±¡å‡ ä¹æ²¡æœ‰é•œé¢åå°„ã€‚

åˆ›å»ºå¯åº”ç”¨äºä¸€ä¸ªæˆ–å¤šä¸ªå¯¹è±¡çš„æè´¨æ›´æœ‰æ„ä¹‰ã€‚æ¯ç§æè´¨éƒ½æœ‰è‡ªå·±çš„ç¯å¢ƒã€æ¼«åå°„å’Œé•œé¢åå°„åˆ†é‡ç³»æ•°ã€‚æˆ‘ä»¬å¯ä»¥ä¸ºæè´¨åˆ›å»ºä¸€ä¸ªç»“æ„ä½“ï¼Œè¯¥ç»“æ„ä½“å°†ä¿å­˜ `Phong` åå°„æ¨¡å‹æ‰€éœ€çš„æ‰€æœ‰ä¿¡æ¯ã€‚

```cpp
struct Material {
  vec3 ambientColor; // k_a * i_a
  vec3 diffuseColor; // k_d * i_d
  vec3 specularColor; // k_s * i_s
  float alpha; // shininess
};
```

æˆ‘ä»¬å°†åˆ›å»ºä¸€ä¸ªå…·æœ‰å¹³é“ºåœ°æ¿å’Œä¸¤ä¸ªçƒä½“çš„åœºæ™¯ã€‚é¦–å…ˆï¼Œæˆ‘ä»¬å°†åˆ›å»ºä¸‰ç§æè´¨ã€‚æˆ‘ä»¬å°†åˆ›å»ºä¸€ä¸ªè¿”å›é‡‘è‰²æè´¨çš„ `gold` å‡½æ•°ã€ä¸€ä¸ªè¿”å›é“¶è‰²æè´¨çš„ `silver` å‡½æ•°å’Œä¸€ä¸ªè¿”å›æ£‹ç›˜å›¾æ¡ˆçš„ `checkerboard` å‡½æ•°ã€‚æ­£å¦‚æ‚¨æ‰€æ–™ï¼Œæ£‹ç›˜æ ¼å›¾æ¡ˆä¸ä¼šå¾ˆé—ªäº®ï¼Œä½†é‡‘å±ä¼šï¼

```cpp
Material gold() {
  vec3 aCol = 0.5 * vec3(0.7, 0.5, 0);
  vec3 dCol = 0.6 * vec3(0.7, 0.7, 0);
  vec3 sCol = 0.6 * vec3(1, 1, 1);
  float a = 5.;

  return Material(aCol, dCol, sCol, a);
}

Material silver() {
  vec3 aCol = 0.4 * vec3(0.8);
  vec3 dCol = 0.5 * vec3(0.7);
  vec3 sCol = 0.6 * vec3(1, 1, 1);
  float a = 5.;

  return Material(aCol, dCol, sCol, a);
}

Material checkerboard(vec3 p) {
  vec3 aCol = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0)) * 0.3;
  vec3 dCol = vec3(0.3);
  vec3 sCol = vec3(0);
  float a = 1.;

  return Material(aCol, dCol, sCol, a);
}
```

æˆ‘ä»¬å°†åˆ›å»ºä¸€ä¸ª `opUnion` å‡½æ•°ï¼Œè¯¥å‡½æ•°çš„ä½œç”¨ä¸æˆ‘ä»¬åœ¨å‰é¢çš„æ•™ç¨‹ä¸­ä½¿ç”¨çš„ `minWithColor` å‡½æ•°ç›¸åŒã€‚

```cpp
Surface opUnion(Surface obj1, Surface obj2) {
  if (obj2.sd < obj1.sd) return obj2;
  return obj1;
}
```

æˆ‘ä»¬çš„åœºæ™¯å°†ä½¿ç”¨ `opUnion` å‡½æ•°å°†å¹³é“ºåœ°æ¿å’Œçƒä½“æ·»åŠ åˆ°åœºæ™¯ä¸­ï¼š

```cpp
Surface scene(vec3 p) {
  Surface sFloor = Surface(1, p.y + 1., checkerboard(p));
  Surface sSphereGold = Surface(2, sdSphere(p - vec3(-2, 0, 0), 1.), gold());
  Surface sSphereSilver = Surface(3, sdSphere(p - vec3(2, 0, 0), 1.), silver());

  Surface co = opUnion(sFloor, sSphereGold);
  co = opUnion(co, sSphereSilver);
  return co;
}
```

æˆ‘ä»¬å°†å‘ `phong` å‡½æ•°æ·»åŠ ä¸€ä¸ªæ¥å— `Material` çš„å‚æ•°ã€‚æ­¤æè´¨å°†ä¿å­˜ `Phong` åå°„æ¨¡å‹çš„æ¯ä¸ªç»„ä»¶æ‰€éœ€çš„æ‰€æœ‰é¢œè‰²å€¼ã€‚

```cpp
vec3 phong(vec3 lightDir, vec3 normal, vec3 rd, Material mat) {
  // ambient
  vec3 ambient = mat.ambientColor;

  // diffuse
  float dotLN = clamp(dot(lightDir, normal), 0., 1.);
  vec3 diffuse = mat.diffuseColor * dotLN;

  // specular
  float dotRV = clamp(dot(reflect(lightDir, normal), -rd), 0., 1.);
  vec3 specular = mat.specularColor * pow(dotRV, mat.alpha);

  return ambient + diffuse + specular;
}
```

åœ¨ `mainImage` å‡½æ•°ä¸­ï¼Œæˆ‘ä»¬å¯ä»¥å°†æœ€è¿‘å¯¹è±¡çš„æè´¨ä¼ é€’ç»™ `phong` å‡½æ•°ã€‚

```cpp
col = lightIntensity1 * phong(lightDirection1, normal, rd, co.mat);
col += lightIntensity2 * phong(lightDirection2, normal , rd, co.mat);
```

å°†æ‰€æœ‰è¿™äº›æ”¾åœ¨ä¸€èµ·ï¼Œæˆ‘ä»¬å¾—åˆ°ä»¥ä¸‹ä»£ç ã€‚
```cpp
const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float PRECISION = 0.001;

float sdSphere(vec3 p, float r )
{
  return length(p) - r;
}

struct Material {
  vec3 ambientColor; // k_a * i_a
  vec3 diffuseColor; // k_d * i_d
  vec3 specularColor; // k_s * i_s
  float alpha; // shininess
};

struct Surface {
  int id; // id of object
  float sd; // signed distance
  Material mat;
};

Material gold() {
  vec3 aCol = 0.5 * vec3(0.7, 0.5, 0);
  vec3 dCol = 0.6 * vec3(0.7, 0.7, 0);
  vec3 sCol = 0.6 * vec3(1, 1, 1);
  float a = 5.;

  return Material(aCol, dCol, sCol, a);
}

Material silver() {
  vec3 aCol = 0.4 * vec3(0.8);
  vec3 dCol = 0.5 * vec3(0.7);
  vec3 sCol = 0.6 * vec3(1, 1, 1);
  float a = 5.;

  return Material(aCol, dCol, sCol, a);
}

Material checkerboard(vec3 p) {
  vec3 aCol = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0)) * 0.3;
  vec3 dCol = vec3(0.3);
  vec3 sCol = vec3(0);
  float a = 1.;

  return Material(aCol, dCol, sCol, a);
}

Surface opUnion(Surface obj1, Surface obj2) {
  if (obj2.sd < obj1.sd) return obj2;
  return obj1;
}

Surface scene(vec3 p) {
  Surface sFloor = Surface(1, p.y + 1., checkerboard(p));
  Surface sSphereGold = Surface(2, sdSphere(p - vec3(-2, 0, 0), 1.), gold());
  Surface sSphereSilver = Surface(3, sdSphere(p - vec3(2, 0, 0), 1.), silver());

  Surface co = opUnion(sFloor, sSphereGold); // closest object
  co = opUnion(co, sSphereSilver);
  return co;
}

Surface rayMarch(vec3 ro, vec3 rd) {
  float depth = MIN_DIST;
  Surface co;

  for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
    vec3 p = ro + depth * rd;
    co = scene(p);
    depth += co.sd;
    if (co.sd < PRECISION || depth > MAX_DIST) break;
  }

  co.sd = depth;

  return co;
}

vec3 calcNormal(vec3 p) {
    vec2 e = vec2(1.0, -1.0) * 0.0005;
    return normalize(
      e.xyy * scene(p + e.xyy).sd +
      e.yyx * scene(p + e.yyx).sd +
      e.yxy * scene(p + e.yxy).sd +
      e.xxx * scene(p + e.xxx).sd);
}

mat3 camera(vec3 cameraPos, vec3 lookAtPoint) {
    vec3 cd = normalize(lookAtPoint - cameraPos); // camera direction
    vec3 cr = normalize(cross(vec3(0, 1, 0), cd)); // camera right
    vec3 cu = normalize(cross(cd, cr)); // camera up

    return mat3(-cr, cu, -cd);
}

vec3 phong(vec3 lightDir, vec3 normal, vec3 rd, Material mat) {
  // ambient
  vec3 ambient = mat.ambientColor;

  // diffuse
  float dotLN = clamp(dot(lightDir, normal), 0., 1.);
  vec3 diffuse = mat.diffuseColor * dotLN;

  // specular
  float dotRV = clamp(dot(reflect(lightDir, normal), -rd), 0., 1.);
  vec3 specular = mat.specularColor * pow(dotRV, mat.alpha);

  return ambient + diffuse + specular;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
  vec3 backgroundColor = mix(vec3(1, .341, .2), vec3(0, 1, 1), uv.y) * 1.6;
  vec3 col = vec3(0);

  vec3 lp = vec3(0); // lookat point (aka camera target)
  vec3 ro = vec3(0, 0, 5);

  vec3 rd = camera(ro, lp) * normalize(vec3(uv, -1)); // ray direction

  Surface co = rayMarch(ro, rd); // closest object

  if (co.sd > MAX_DIST) {
    col = backgroundColor;
  } else {
      vec3 p = ro + rd * co.sd; // point on surface found by ray marching
      vec3 normal = calcNormal(p); // surface normal

      // light #1
      vec3 lightPosition1 = vec3(-8, -6, -5);
      vec3 lightDirection1 = normalize(lightPosition1 - p);
      float lightIntensity1 = 0.9;

      // light #2
      vec3 lightPosition2 = vec3(1, 1, 1);
      vec3 lightDirection2 = normalize(lightPosition2 - p);
      float lightIntensity2 = 0.5;

      // final color of object
      col = lightIntensity1 * phong(lightDirection1, normal, rd, co.mat);
      col += lightIntensity2 * phong(lightDirection2, normal , rd, co.mat);
  }

  fragColor = vec4(col, 1.0);
}
```

å½“æˆ‘ä»¬è¿è¡Œè¿™æ®µä»£ç æ—¶ï¼Œæˆ‘ä»¬åº”è¯¥çœ‹åˆ°ä¸€ä¸ªé‡‘è‰²çš„çƒä½“å’Œé“¶è‰²çš„çƒä½“æ¼‚æµ®åœ¨æ—¥è½å‰ã€‚Gorgeousï¼

<p align="center">![-](./img/img-10.png)</p>

## ç»“è®º
åœ¨æœ¬è¯¾ä¸­ï¼Œæˆ‘ä»¬å­¦ä¹ äº† `Phong` åå°„æ¨¡å‹å¦‚ä½•é€šè¿‡å‘å¯¹è±¡æ·»åŠ ä¸€ç‚¹çœ©å…‰æˆ–å…‰æ³½æ¥çœŸæ­£æ”¹å–„åœºæ™¯çš„å¤–è§‚ã€‚æˆ‘ä»¬è¿˜å­¦ä¹ äº†å¦‚ä½•ä½¿ç”¨ç»“æ„ä½“ä¸ºåœºæ™¯ä¸­çš„æ¯ä¸ªå¯¹è±¡åˆ†é…ä¸åŒçš„æè´¨ã€‚ç¡®ä¿ç€è‰²å™¨å¾ˆæœ‰è¶£ï¼ğŸ˜ƒ