---
sidebar_position: 1
---

# 5.5 æ›´å¤š sdf å½¢çŠ¶

[**Inigo Quilez**](https://www.iquilezles.org/) åˆ›å»ºäº†è®¸å¤š 2D SDF å’Œ 3D SDFï¼Œä¾› Shadertoy çš„å¼€å‘äººå‘˜ä½¿ç”¨ã€‚åœ¨æœ¬èŠ‚ä¸­ï¼Œæˆ‘å°†è®¨è®ºå¦‚ä½•ä½¿ç”¨ä»–çš„ [**2D SDF**](https://www.iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm) åˆ—è¡¨ä»¥åŠæˆ‘ä»¬åœ¨ Shadertoy ç³»åˆ—çš„ç¬¬ 4 éƒ¨åˆ†ä¸­å­¦åˆ°çš„æŠ€æœ¯æ¥ç»˜åˆ¶ 2D å½¢çŠ¶ã€‚

ä½¿ç”¨ SDF åˆ›å»ºå½¢çŠ¶æ—¶ï¼Œå®ƒä»¬é€šå¸¸ç§°ä¸º**åŸºå…ƒ(primitives)**ï¼Œå› ä¸ºå®ƒä»¬æ„æˆäº†åˆ›å»ºæ›´æŠ½è±¡å½¢çŠ¶çš„æ„å»ºå—ã€‚å¯¹äº 2Dï¼Œåœ¨ç”»å¸ƒä¸Šç»˜åˆ¶å½¢çŠ¶éå¸¸ç®€å•ï¼Œä½†å½“æˆ‘ä»¬è®¨è®º 3D å½¢çŠ¶æ—¶ï¼Œå®ƒä¼šå˜å¾—æ›´åŠ å¤æ‚ã€‚

## ç»˜åˆ¶ star
è®©æˆ‘ä»¬ç”¨æ˜Ÿæ˜Ÿ SDF ç»ƒä¹ ï¼Œå› ä¸ºç”»æ˜Ÿæ˜Ÿæ€»æ˜¯å¾ˆæœ‰è¶£ã€‚å¯¼èˆªåˆ° Inigo Quilez çš„[**ç½‘ç«™**](https://www.iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm)å¹¶å‘ä¸‹æ»šåŠ¨åˆ°åä¸ºâ€œStar 5 - exactâ€çš„ SDFã€‚å®ƒåº”å…·æœ‰ä»¥ä¸‹å®šä¹‰ï¼š

```cpp
float sdStar5(in vec2 p, in float r, in float rf)
{
  const vec2 k1 = vec2(0.809016994375, -0.587785252292);
  const vec2 k2 = vec2(-k1.x,k1.y);
  p.x = abs(p.x);
  p -= 2.0*max(dot(k1,p),0.0)*k1;
  p -= 2.0*max(dot(k2,p),0.0)*k2;
  p.x = abs(p.x);
  p.y -= r;
  vec2 ba = rf*vec2(-k1.y,k1.x) - vec2(0,1);
  float h = clamp( dot(p,ba)/dot(ba,ba), 0.0, r );
  return length(p-ba*h) * sign(p.y*ba.x-p.x*ba.y);
}
```

ä¸ç”¨æ‹…å¿ƒå‡½æ•°ä¸­çš„ **in** é™å®šç¬¦ã€‚å¦‚æœéœ€è¦ï¼Œæ‚¨å¯ä»¥åˆ é™¤å®ƒä»¬ï¼Œå› ä¸ºå¦‚æœæœªæŒ‡å®šä»»ä½•æ¡ä»¶ï¼Œåˆ™ **in** æ˜¯é»˜è®¤é™å®šç¬¦ã€‚

è®©æˆ‘ä»¬ä½¿ç”¨ä»¥ä¸‹ä»£ç åˆ›å»ºä¸€ä¸ªæ–°çš„ Shadertoy ç€è‰²å™¨ï¼š

```cpp
float sdStar5(in vec2 p, in float r, in float rf)
{
  const vec2 k1 = vec2(0.809016994375, -0.587785252292);
  const vec2 k2 = vec2(-k1.x,k1.y);
  p.x = abs(p.x);
  p -= 2.0*max(dot(k1,p),0.0)*k1;
  p -= 2.0*max(dot(k2,p),0.0)*k2;
  p.x = abs(p.x);
  p.y -= r;
  vec2 ba = rf*vec2(-k1.y,k1.x) - vec2(0,1);
  float h = clamp( dot(p,ba)/dot(ba,ba), 0.0, r );
  return length(p-ba*h) * sign(p.y*ba.x-p.x*ba.y);
}

vec3 drawScene(vec2 uv) {
  vec3 col = vec3(0);
  float star = sdStar5(uv, 0.12, 0.45);

  col = mix(vec3(1, 1, 0), col, step(0., star));

  return col;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = fragCoord/iResolution.xy; // <0, 1>
  uv -= 0.5; // <-0.5,0.5>
  uv.x *= iResolution.x/iResolution.y; // fix aspect ratio

  vec3 col = drawScene(uv);

  // Output to screen
  fragColor = vec4(col,1.0);
}
```

å½“æˆ‘ä»¬è¿è¡Œè¿™æ®µä»£ç æ—¶ï¼Œæ‚¨åº”è¯¥èƒ½å¤Ÿçœ‹åˆ°ä¸€ä¸ªäº®é»„è‰²çš„æ˜Ÿæ˜Ÿï¼â­

<p align="center">![-](./img/img-19.png)</p>

ä¸è¿‡ï¼Œè¿˜ç¼ºå°‘ä¸€ä»¶äº‹ã€‚æˆ‘ä»¬éœ€è¦åœ¨ sdStar5 å‡½æ•°çš„å¼€å¤´æ·»åŠ ä¸€ä¸ªåç§»é‡ï¼Œå°† UV åæ ‡ç¨å¾®ç§»åŠ¨ä¸€ä¸‹ã€‚æˆ‘ä»¬å¯ä»¥æ·»åŠ ä¸€ä¸ªåä¸º offset çš„æ–°å‚æ•°ï¼Œæˆ‘ä»¬å¯ä»¥ä»å‘é‡ p ä¸­å‡å»è¿™ä¸ªåç§»é‡ï¼Œå®ƒè¡¨ç¤ºæˆ‘ä»¬ä¼ é€’ç»™æ­¤å‡½æ•°çš„ UV åæ ‡ã€‚

æˆ‘ä»¬å®Œæˆçš„ä»£ç åº”è¯¥æ˜¯è¿™æ ·çš„ï¼š

```cpp
float sdStar5(in vec2 p, in float r, in float rf, vec2 offset)
{
  p -= offset; // This will subtract offset.x from p.x and subtract offset.y from p.y
  const vec2 k1 = vec2(0.809016994375, -0.587785252292);
  const vec2 k2 = vec2(-k1.x,k1.y);
  p.x = abs(p.x);
  p -= 2.0*max(dot(k1,p),0.0)*k1;
  p -= 2.0*max(dot(k2,p),0.0)*k2;
  p.x = abs(p.x);
  p.y -= r;
  vec2 ba = rf*vec2(-k1.y,k1.x) - vec2(0,1);
  float h = clamp( dot(p,ba)/dot(ba,ba), 0.0, r );
  return length(p-ba*h) * sign(p.y*ba.x-p.x*ba.y);
}

vec3 drawScene(vec2 uv) {
  vec3 col = vec3(0);
  float star = sdStar5(uv, 0.12, 0.45, vec2(0.2, 0)); // Add an offset to shift the star's position

  col = mix(vec3(1, 1, 0), col, step(0., star));

  return col;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = fragCoord/iResolution.xy; // <0, 1>
  uv -= 0.5; // <-0.5,0.5>
  uv.x *= iResolution.x/iResolution.y; // fix aspect ratio

  vec3 col = drawScene(uv);

  // Output to screen
  fragColor = vec4(col,1.0);
}
```

## ä½¿ç”¨ sdBox SDF
ç»˜åˆ¶æ–¹æ¡†/çŸ©å½¢å¾ˆå¸¸è§ï¼Œå› æ­¤æˆ‘ä»¬å°†é€‰æ‹©æ ‡é¢˜ä¸ºâ€œæ–¹æ¡† - ç²¾ç¡®â€çš„ SDFã€‚å®ƒå…·æœ‰ä»¥ä¸‹å®šä¹‰ï¼š

```cpp
float sdBox( in vec2 p, in vec2 b )
{
  vec2 d = abs(p)-b;
  return length(max(d,0.0)) + min(max(d.x,d.y),0.0);
}
```

æˆ‘ä»¬å°†å‘å‡½æ•°å£°æ˜æ·»åŠ ä¸€ä¸ª offset å‚æ•°ã€‚

```cpp
float sdBox( in vec2 p, in vec2 b, vec2 offset )
{
  p -= offset;
  vec2 d = abs(p)-b;
  return length(max(d,0.0)) + min(max(d.x,d.y),0.0);
}
```

ç°åœ¨ï¼Œæˆ‘ä»¬åº”è¯¥èƒ½å¤Ÿæ¯«æ— é—®é¢˜åœ°æ¸²æŸ“ box å’Œ starï¼š

```cpp
float sdBox( in vec2 p, in vec2 b, vec2 offset )
{
  p -= offset;
  vec2 d = abs(p)-b;
  return length(max(d,0.0)) + min(max(d.x,d.y),0.0);
}

float sdStar5(in vec2 p, in float r, in float rf, vec2 offset)
{
  p -= offset; // This will subtract offset.x from p.x and subtract offset.y from p.y
  const vec2 k1 = vec2(0.809016994375, -0.587785252292);
  const vec2 k2 = vec2(-k1.x,k1.y);
  p.x = abs(p.x);
  p -= 2.0*max(dot(k1,p),0.0)*k1;
  p -= 2.0*max(dot(k2,p),0.0)*k2;
  p.x = abs(p.x);
  p.y -= r;
  vec2 ba = rf*vec2(-k1.y,k1.x) - vec2(0,1);
  float h = clamp( dot(p,ba)/dot(ba,ba), 0.0, r );
  return length(p-ba*h) * sign(p.y*ba.x-p.x*ba.y);
}

vec3 drawScene(vec2 uv) {
  vec3 col = vec3(0);
  float box = sdBox(uv, vec2(0.2, 0.1), vec2(-0.2, 0));
  float star = sdStar5(uv, 0.12, 0.45, vec2(0.2, 0));

  col = mix(vec3(1, 1, 0), col, step(0., star));
  col = mix(vec3(0, 0, 1), col, step(0., box));

  return col;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = fragCoord/iResolution.xy; // <0, 1>
  uv -= 0.5; // <-0.5,0.5>
  uv.x *= iResolution.x/iResolution.y; // fix aspect ratio

  vec3 col = drawScene(uv);

  // Output to screen
  fragColor = vec4(col,1.0);
}
```
<p align="center">![-](./img/img-20.png)</p>

åªéœ€è¿›è¡Œä¸€äº›å°çš„è°ƒæ•´ï¼Œæˆ‘ä»¬å°±å¯ä»¥ä» Inigo Quilez çš„ç½‘ç«™ä¸ŠæŒ‘é€‰è®¸å¤š 2D SDFï¼Œå¹¶å°†å®ƒä»¬ä»¥åç§»é‡ç»˜åˆ¶åˆ°ç”»å¸ƒä¸Šã€‚

ä½†æ˜¯è¯·æ³¨æ„ï¼ŒæŸäº› SDF éœ€è¦åœ¨ä»–çš„ 3D SDF é¡µé¢ä¸Šå®šä¹‰çš„å‡½æ•°ï¼š

```cpp
float dot2( in vec2 v ) { return dot(v,v); }
float dot2( in vec3 v ) { return dot(v,v); }
float ndot( in vec2 a, in vec2 b ) { return a.x*b.x - a.y*b.y; }
```
## ä½¿ç”¨ sdSegment SDF
Inigo Quilez [**ç½‘ç«™**](https://www.iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm)ä¸Šçš„ä¸€äº› 2D SDF ç”¨äºçº¿æ®µæˆ–æ›²çº¿ï¼Œå› æ­¤æˆ‘ä»¬å¯èƒ½éœ€è¦ç¨å¾®æ”¹å˜æˆ‘ä»¬çš„æ–¹æ³•ã€‚è®©æˆ‘ä»¬çœ‹çœ‹æ ‡é¢˜ä¸ºâ€œSegment - exactï¼ˆçº¿æ®µ - ç²¾ç¡®ï¼‰â€çš„ SDFã€‚å®ƒå…·æœ‰ä»¥ä¸‹å®šä¹‰ï¼š

```cpp
float sdSegment( in vec2 p, in vec2 a, in vec2 b )
{
  vec2 pa = p-a, ba = b-a;
  float h = clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1.0 );
  return length( pa - ba*h );
}
```

è®©æˆ‘ä»¬å°è¯•ä½¿ç”¨è¿™ä¸ª SDFï¼Œçœ‹çœ‹ä¼šå‘ç”Ÿä»€ä¹ˆã€‚

```cpp
float sdSegment( in vec2 p, in vec2 a, in vec2 b )
{
  vec2 pa = p-a, ba = b-a;
  float h = clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1.0 );
  return length( pa - ba*h );
}

vec3 drawScene(vec2 uv) {
  vec3 col = vec3(0);
  float segment = sdSegment(uv, vec2(0, 0), vec2(0, .2));

  col = mix(vec3(1, 1, 1), col, step(0., segment));

  return col;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = fragCoord/iResolution.xy; // <0, 1>
  uv -= 0.5; // <-0.5,0.5>
  uv.x *= iResolution.x/iResolution.y; // fix aspect ratio

  vec3 col = drawScene(uv);

  // Output to screen
  fragColor = vec4(col,1.0);
}
```

å½“æˆ‘ä»¬è¿è¡Œæ­¤ä»£ç æ—¶ï¼Œæˆ‘ä»¬å°†çœ‹åˆ°ä¸€ä¸ªå®Œå…¨é»‘è‰²çš„ç”»å¸ƒã€‚ä¸€äº› SDF è¦æ±‚æˆ‘ä»¬æ›´ä»”ç»†åœ°æŸ¥çœ‹ä»£ç ã€‚ç›®å‰ï¼Œè¯¥åŒºæ®µå¤ªç»†ï¼Œæ— æ³•åœ¨æˆ‘ä»¬çš„ç”»å¸ƒä¸­çœ‹åˆ°ã€‚ä¸ºäº†ç»™çº¿æ®µä¸€äº›åšåº¦ï¼Œæˆ‘ä»¬å¯ä»¥ä»è¿”å›çš„è·ç¦»ä¸­å‡å»ä¸€ä¸ªå€¼ã€‚

```cpp
float sdSegment( in vec2 p, in vec2 a, in vec2 b )
{
  vec2 pa = p-a, ba = b-a;
  float h = clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1.0 );
  return length( pa - ba*h );
}

vec3 drawScene(vec2 uv) {
  vec3 col = vec3(0);
  float segment = sdSegment(uv, vec2(0, 0), vec2(0, 0.2));

  col = mix(vec3(1, 1, 1), col, step(0., segment - 0.02)); // Subtract 0.02 from the returned "signed distance" value of the segment

  return col;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = fragCoord/iResolution.xy; // <0, 1>
  uv -= 0.5; // <-0.5,0.5>
  uv.x *= iResolution.x/iResolution.y; // fix aspect ratio

  vec3 col = drawScene(uv);

  // Output to screen
  fragColor = vec4(col,1.0);
}
```

ç°åœ¨ï¼Œæˆ‘ä»¬å¯ä»¥çœ‹åˆ°æˆ‘ä»¬çš„åŒºæ®µå‡ºç°ï¼å®ƒä»åæ ‡ ï¼ˆ0, 0) å¼€å§‹ï¼Œåˆ° (0, 0.2) ç»“æŸã€‚åœ¨å¯¹ sdSegment å‡½æ•°çš„è°ƒç”¨ä¸­ï¼Œä½¿ç”¨è¾“å…¥å‘é‡ a å’Œ bï¼Œä»¥ä¸åŒçš„æ–¹å¼ç§»åŠ¨å’Œæ‹‰ä¼¸çº¿æ®µã€‚å¦‚æœè¦ä½¿æ®µæ›´ç»†æˆ–æ›´å®½ï¼Œå¯ä»¥å°† 0.02 æ›¿æ¢ä¸ºå¦ä¸€ä¸ªæ•°å­—ã€‚


<p align="center">![-](./img/img-21.png)</p>

æ‚¨è¿˜å¯ä»¥ä½¿ç”¨ **smoothstep** å‡½æ•°ä½¿çº¿æ®µåœ¨è¾¹ç¼˜å‘¨å›´çœ‹èµ·æ¥æ¨¡ç³Šã€‚

```cpp
float sdSegment( in vec2 p, in vec2 a, in vec2 b )
{
  vec2 pa = p-a, ba = b-a;
  float h = clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1.0 );
  return length( pa - ba*h );
}

vec3 drawScene(vec2 uv) {
  vec3 col = vec3(0);
  float segment = sdSegment(uv, vec2(0, 0), vec2(0, .2));

  col = mix(vec3(1, 1, 1), col, smoothstep(0., 0.02, segment));

  return col;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = fragCoord/iResolution.xy; // <0, 1>
  uv -= 0.5; // <-0.5,0.5>
  uv.x *= iResolution.x/iResolution.y; // fix aspect ratio

  vec3 col = drawScene(uv);

  // Output to screen
  fragColor = vec4(col,1.0);
}
```

ç°åœ¨çœ‹èµ·æ¥æ­£åœ¨å‘å…‰ï¼

<p align="center">![-](./img/img-22.png)</p>

## ä½¿ç”¨ sdBezier SDF

Inigo Quilez çš„ç½‘ç«™ä¹Ÿæœ‰è´å¡å°”æ›²çº¿çš„ SDFã€‚æ›´å…·ä½“åœ°è¯´ï¼Œä»–æœ‰ä¸€ä¸ªäºŒæ¬¡è´å¡å°”æ›²çº¿çš„ SDFã€‚æŸ¥æ‰¾æ ‡é¢˜ä¸ºâ€œQuadratic Bezier - exactâ€ï¼ˆäºŒæ¬¡è´å¡å°”æ›²çº¿ - ç²¾ç¡®ï¼‰çš„ SDFã€‚å®ƒå…·æœ‰ä»¥ä¸‹å®šä¹‰ï¼š

```cpp
float sdBezier( in vec2 pos, in vec2 A, in vec2 B, in vec2 C )
{
    vec2 a = B - A;
    vec2 b = A - 2.0*B + C;
    vec2 c = a * 2.0;
    vec2 d = A - pos;
    float kk = 1.0/dot(b,b);
    float kx = kk * dot(a,b);
    float ky = kk * (2.0*dot(a,a)+dot(d,b)) / 3.0;
    float kz = kk * dot(d,a);
    float res = 0.0;
    float p = ky - kx*kx;
    float p3 = p*p*p;
    float q = kx*(2.0*kx*kx-3.0*ky) + kz;
    float h = q*q + 4.0*p3;
    if( h >= 0.0)
    {
        h = sqrt(h);
        vec2 x = (vec2(h,-h)-q)/2.0;
        vec2 uv = sign(x)*pow(abs(x), vec2(1.0/3.0));
        float t = clamp( uv.x+uv.y-kx, 0.0, 1.0 );
        res = dot2(d + (c + b*t)*t);
    }
    else
    {
        float z = sqrt(-p);
        float v = acos( q/(p*z*2.0) ) / 3.0;
        float m = cos(v);
        float n = sin(v)*1.732050808;
        vec3  t = clamp(vec3(m+m,-n-m,n-m)*z-kx,0.0,1.0);
        res = min( dot2(d+(c+b*t.x)*t.x),
                   dot2(d+(c+b*t.y)*t.y) );
        // the third root cannot be the closest
        // res = min(res,dot2(d+(c+b*t.z)*t.z));
    }
    return sqrt( res );
}
```

è¿™æ˜¯ä¸€ä¸ªç›¸å½“å¤§çš„å‡½æ•°ï¼è¯·æ³¨æ„ï¼Œæ­¤å‡½æ•°ä½¿ç”¨å®ç”¨å‡½æ•° dot2ã€‚è¿™åœ¨ä»–çš„ 3D SDF é¡µé¢ä¸Šå®šä¹‰ã€‚

äºŒæ¬¡è´å¡å°”æ›²çº¿æ¥å—ä¸‰ä¸ªæ§åˆ¶ç‚¹ã€‚åœ¨ 2D ä¸­ï¼Œæ¯ä¸ªæ§åˆ¶ç‚¹éƒ½å°†æ˜¯ä¸€ä¸ªå…·æœ‰ x åˆ†é‡å’Œ y åˆ†é‡çš„ vec2 å€¼ã€‚æ‚¨å¯ä»¥ä½¿ç”¨æˆ‘åœ¨ Desmos ä¸Šåˆ›å»ºçš„å›¾è¡¨æ¥è°ƒæ•´æ§åˆ¶ç‚¹ã€‚

ä¸ sdSegment ä¸€æ ·ï¼Œæˆ‘ä»¬å¿…é¡»ä»è¿”å›çš„ â€œsigned distanceâ€ ä¸­å‡å»ä¸€ä¸ªå°å€¼æ‰èƒ½æ­£ç¡®æŸ¥çœ‹æ›²çº¿ã€‚è®©æˆ‘ä»¬çœ‹çœ‹å¦‚ä½•ä½¿ç”¨ GLSL ä»£ç ç»˜åˆ¶äºŒæ¬¡è´å¡å°”æ›²çº¿ï¼š

```cpp
float dot2( in vec2 v ) { return dot(v,v); }

float sdBezier( in vec2 pos, in vec2 A, in vec2 B, in vec2 C )
{
    vec2 a = B - A;
    vec2 b = A - 2.0*B + C;
    vec2 c = a * 2.0;
    vec2 d = A - pos;
    float kk = 1.0/dot(b,b);
    float kx = kk * dot(a,b);
    float ky = kk * (2.0*dot(a,a)+dot(d,b)) / 3.0;
    float kz = kk * dot(d,a);
    float res = 0.0;
    float p = ky - kx*kx;
    float p3 = p*p*p;
    float q = kx*(2.0*kx*kx-3.0*ky) + kz;
    float h = q*q + 4.0*p3;
    if( h >= 0.0)
    {
        h = sqrt(h);
        vec2 x = (vec2(h,-h)-q)/2.0;
        vec2 uv = sign(x)*pow(abs(x), vec2(1.0/3.0));
        float t = clamp( uv.x+uv.y-kx, 0.0, 1.0 );
        res = dot2(d + (c + b*t)*t);
    }
    else
    {
        float z = sqrt(-p);
        float v = acos( q/(p*z*2.0) ) / 3.0;
        float m = cos(v);
        float n = sin(v)*1.732050808;
        vec3  t = clamp(vec3(m+m,-n-m,n-m)*z-kx,0.0,1.0);
        res = min( dot2(d+(c+b*t.x)*t.x),
                   dot2(d+(c+b*t.y)*t.y) );
        // the third root cannot be the closest
        // res = min(res,dot2(d+(c+b*t.z)*t.z));
    }
    return sqrt( res );
}

vec3 drawScene(vec2 uv) {
    vec3 col = vec3(0);
    vec2 A = vec2(0, 0);
    vec2 B = vec2(0.2, 0);
    vec2 C = vec2(0.2, 0.2);
    float curve = sdBezier(uv, A, B, C);

    col = mix(vec3(1, 1, 1), col, step(0., curve - 0.01));

    return col;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec2 uv = fragCoord/iResolution.xy; // <0, 1>
    uv -= 0.5; // <-0.5,0.5>
    uv.x *= iResolution.x/iResolution.y; // fix aspect ratio

    vec3 col = drawScene(uv);

    // Output to screen
    fragColor = vec4(col,1.0);
}
```

è¿è¡Œä»£ç æ—¶ï¼Œæ‚¨åº”è¯¥ä¼šçœ‹åˆ°äºŒæ¬¡è´å¡å°”æ›²çº¿å‡ºç°ã€‚

<p align="center">![-](./img/img-23.png)</p>

å°è¯•ç©å¼„æ§åˆ¶ç‚¹ï¼è®°å¾—ï¼æ‚¨å¯ä»¥ä½¿ç”¨æˆ‘çš„ [**Desmos graph**](https://www.desmos.com/calculator/xdhowj4wkk) æ¥æä¾›å¸®åŠ©ï¼

æ‚¨å¯ä»¥å°† 2D æ“ä½œä¸ Bezier æ›²çº¿ä¸€èµ·ä½¿ç”¨ï¼Œä»¥åˆ›å»ºæœ‰è¶£çš„æ•ˆæœã€‚æˆ‘ä»¬å¯ä»¥ä»ä¸€ä¸ªåœ†ä¸­å‡å»ä¸¤æ¡è´å¡å°”æ›²çº¿ï¼Œå¾—åˆ°æŸç§ç½‘çƒğŸ¾ã€‚ç”±æ‚¨æ¥æ¢ç´¢æ‚¨å¯ä»¥ä½¿ç”¨å‘ˆç°ç»™æ‚¨çš„å·¥å…·åˆ›å»ºçš„æ‰€æœ‰å†…å®¹ï¼

<p align="center">![-](./img/img-24.png)</p>

æ‚¨å¯ä»¥åœ¨ä¸‹é¢æ‰¾åˆ°ç”¨äºåˆ¶ä½œç½‘çƒçš„å®Œæˆä»£ç ï¼š

```cpp
vec3 getBackgroundColor(vec2 uv) {
  uv = uv * 0.5 + 0.5; // remap uv from <-0.5,0.5> to <0.25,0.75>
  vec3 gradientStartColor = vec3(1., 0., 1.);
  vec3 gradientEndColor = vec3(0., 1., 1.);
  return mix(gradientStartColor, gradientEndColor, uv.y); // gradient goes from bottom to top
}

float sdCircle(vec2 uv, float r, vec2 offset) {
  float x = uv.x - offset.x;
  float y = uv.y - offset.y;

  return length(vec2(x, y)) - r;
}

float dot2( in vec2 v ) { return dot(v,v); }

float sdBezier( in vec2 pos, in vec2 A, in vec2 B, in vec2 C )
{
    vec2 a = B - A;
    vec2 b = A - 2.0*B + C;
    vec2 c = a * 2.0;
    vec2 d = A - pos;
    float kk = 1.0/dot(b,b);
    float kx = kk * dot(a,b);
    float ky = kk * (2.0*dot(a,a)+dot(d,b)) / 3.0;
    float kz = kk * dot(d,a);
    float res = 0.0;
    float p = ky - kx*kx;
    float p3 = p*p*p;
    float q = kx*(2.0*kx*kx-3.0*ky) + kz;
    float h = q*q + 4.0*p3;
    if( h >= 0.0)
    {
        h = sqrt(h);
        vec2 x = (vec2(h,-h)-q)/2.0;
        vec2 uv = sign(x)*pow(abs(x), vec2(1.0/3.0));
        float t = clamp( uv.x+uv.y-kx, 0.0, 1.0 );
        res = dot2(d + (c + b*t)*t);
    }
    else
    {
        float z = sqrt(-p);
        float v = acos( q/(p*z*2.0) ) / 3.0;
        float m = cos(v);
        float n = sin(v)*1.732050808;
        vec3  t = clamp(vec3(m+m,-n-m,n-m)*z-kx,0.0,1.0);
        res = min( dot2(d+(c+b*t.x)*t.x),
                   dot2(d+(c+b*t.y)*t.y) );
        // the third root cannot be the closest
        // res = min(res,dot2(d+(c+b*t.z)*t.z));
    }
    return sqrt( res );
}

vec3 drawScene(vec2 uv) {
  vec3 col = getBackgroundColor(uv);
  float d1 = sdCircle(uv, 0.2, vec2(0., 0.));
  vec2 A = vec2(-0.2, 0.2);
  vec2 B = vec2(0, 0);
  vec2 C = vec2(0.2, 0.2);
  float d2 = sdBezier(uv, A, B, C) - 0.03;
  float d3 = sdBezier(uv*vec2(1,-1), A, B, C) - 0.03;

  float res; // result
  res = max(d1, -d2); // subtraction - subtract d2 from d1
  res = max(res, -d3); // subtraction - subtract d3 from the result

  res = smoothstep(0., 0.01, res); // antialias entire result

  col = mix(vec3(.8,.9,.2), col, res);
  return col;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = fragCoord/iResolution.xy; // <0, 1>
  uv -= 0.5; // <-0.5,0.5>
  uv.x *= iResolution.x/iResolution.y; // fix aspect ratio

  vec3 col = drawScene(uv);

  fragColor = vec4(col,1.0); // Output to screen
}
```

##  ç»“è®º
åœ¨æœ¬æ•™ç¨‹ä¸­ï¼Œæˆ‘ä»¬å­¦ä¹ äº†å¦‚ä½•é€šè¿‡ç»˜åˆ¶å¿ƒå½¢â¤ï¸å’Œå…¶ä»–å½¢çŠ¶æ¥è¡¨è¾¾å¯¹ç€è‰²å™¨çš„æ›´å¤šçˆ±ã€‚æˆ‘ä»¬å­¦ä¹ äº†å¦‚ä½•ç»˜åˆ¶æ˜Ÿå½¢ã€çº¿æ®µå’ŒäºŒæ¬¡è´å¡å°”æ›²çº¿ã€‚å½“ç„¶ï¼Œæˆ‘ä½¿ç”¨ 2D SDF ç»˜åˆ¶å½¢çŠ¶çš„æŠ€æœ¯åªæ˜¯ä¸ªäººå–œå¥½ã€‚æˆ‘ä»¬å¯ä»¥é€šè¿‡å¤šç§æ–¹å¼å°† 2D å½¢çŠ¶ç»˜åˆ¶åˆ°ç”»å¸ƒä¸Šã€‚æˆ‘ä»¬è¿˜å­¦ä¹ äº†å¦‚ä½•å°†åŸºå…ƒå½¢çŠ¶ç»„åˆåœ¨ä¸€èµ·ä»¥åˆ›å»ºæ›´å¤æ‚çš„å½¢çŠ¶ã€‚åœ¨ä¸‹ä¸€ç¯‡æ–‡ç« ä¸­ï¼Œæˆ‘ä»¬å°†å¼€å§‹å­¦ä¹ å¦‚ä½•ä½¿ç”¨ raymarching ç»˜åˆ¶ 3D å½¢çŠ¶å’Œåœºæ™¯ï¼ğŸ‰