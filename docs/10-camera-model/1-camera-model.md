---
sidebar_position: 1
---

# 10.1 å¸¦æœ‰è§‚å¯Ÿç‚¹çš„ç›¸æœºæ¨¡å‹

## åˆå§‹è®¾ç½®
è®©æˆ‘ä»¬åˆ›å»ºä¸€ä¸ªæ–°çš„ç€è‰²å™¨ï¼Œå¹¶æ·»åŠ ä»¥ä¸‹æˆ‘ä»¬å°†ç”¨äºæœ¬æ•™ç¨‹çš„æ ·æ¿ä»£ç ã€‚è¯·æ³¨æ„ç°åœ¨å¦‚ä½•åœ¨ä»£ç é¡¶éƒ¨å®šä¹‰å¸¸é‡ã€‚

```cpp
// Constants
const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float PRECISION = 0.001;
const float EPSILON = 0.0005;
const float PI = 3.14159265359;

// Rotation matrix around the X axis.
mat3 rotateX(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(1, 0, 0),
        vec3(0, c, -s),
        vec3(0, s, c)
    );
}

// Rotation matrix around the Y axis.
mat3 rotateY(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, 0, s),
        vec3(0, 1, 0),
        vec3(-s, 0, c)
    );
}

// Rotation matrix around the Z axis.
mat3 rotateZ(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, -s, 0),
        vec3(s, c, 0),
        vec3(0, 0, 1)
    );
}

// Identity matrix.
mat3 identity() {
    return mat3(
        vec3(1, 0, 0),
        vec3(0, 1, 0),
        vec3(0, 0, 1)
    );
}

struct Surface {
    float sd; // signed distance value
    vec3 col; // color
};

Surface sdBox( vec3 p, vec3 b, vec3 offset, vec3 col, mat3 transform)
{
  p = (p - offset) * transform; // apply transformation matrix
  vec3 q = abs(p) - b;
  float d = length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
  return Surface(d, col);
}

Surface sdFloor(vec3 p, vec3 col) {
  float d = p.y + 1.;
  return Surface(d, col);
}

Surface minWithColor(Surface obj1, Surface obj2) {
  if (obj2.sd < obj1.sd) return obj2;
  return obj1;
}

Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0));
  Surface co = sdFloor(p, floorColor);
  co = minWithColor(co, sdBox(p, vec3(1), vec3(-4, 0.5, -4), vec3(1, 0, 0), identity())); // left cube
  co = minWithColor(co, sdBox(p, vec3(1), vec3(0, 0.5, -4), vec3(0, 0.65, 0.2), identity())); // center cube
  co = minWithColor(co, sdBox(p, vec3(1), vec3(4, 0.5, -4), vec3(0, 0.55, 2), identity())); // right cube
  return co;
}

Surface rayMarch(vec3 ro, vec3 rd, float start, float end) {
  float depth = start;
  Surface co; // closest object

  for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
    vec3 p = ro + depth * rd;
    co = sdScene(p);
    depth += co.sd;
    if (co.sd < PRECISION || depth > end) break;
  }

  co.sd = depth;

  return co;
}

vec3 calcNormal(in vec3 p) {
    vec2 e = vec2(1, -1) * EPSILON;
    return normalize(
      e.xyy * sdScene(p + e.xyy).sd +
      e.yyx * sdScene(p + e.yyx).sd +
      e.yxy * sdScene(p + e.yxy).sd +
      e.xxx * sdScene(p + e.xxx).sd);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
  vec3 backgroundColor = vec3(0.835, 1, 1);

  vec3 col = vec3(0);
  vec3 ro = vec3(0, 0, 3); // ray origin that represents camera position
  vec3 rd = normalize(vec3(uv, -1)); // ray direction

  Surface co = rayMarch(ro, rd, MIN_DIST, MAX_DIST); // closest object

  if (co.sd > MAX_DIST) {
    col = backgroundColor; // ray didn't hit anything
  } else {
    vec3 p = ro + rd * co.sd; // point on cube or floor we discovered from ray marching
    vec3 normal = calcNormal(p);
    vec3 lightPosition = vec3(2, 2, 7);
    vec3 lightDirection = normalize(lightPosition - p);

    float dif = clamp(dot(normal, lightDirection), 0.3, 1.); // diffuse reflection

    col = dif * co.col + backgroundColor * .2; // Add a bit of background color to the diffuse color
  }

  // Output to screen
  fragColor = vec4(col, 1.0);
}
```

æ­¤ä»£ç å°†ç”Ÿæˆä¸€ä¸ªåŒ…å«ä¸‰ä¸ªç«‹æ–¹ä½“çš„åœºæ™¯ï¼Œæ¯ä¸ªç«‹æ–¹ä½“å…·æœ‰ä¸åŒçš„é¢œè‰²ï¼šçº¢è‰²ã€ç»¿è‰²å’Œè“è‰²ã€‚

<p align="center">![-](./img/img-1.png)</p>

## LookAt ç‚¹
ç›®å‰ï¼Œå½“æˆ‘ä»¬æƒ³è¦ç§»åŠ¨ç›¸æœºæ—¶ï¼Œæˆ‘ä»¬å¿…é¡»è°ƒæ•´å…‰çº¿åŸç‚¹çš„å€¼ã€‚è¦å€¾æ–œç›¸æœºï¼Œæˆ‘ä»¬éœ€è¦å°†å…‰çº¿æ–¹å‘ä¹˜ä»¥æ—‹è½¬çŸ©é˜µã€‚

å¦ä¸€ç§æ–¹æ³•æ˜¯åˆ›å»ºä¸€ä¸ªæ¥å—ç›¸æœºä½ç½®ï¼ˆæˆ–å…‰çº¿åŸç‚¹ï¼‰å’Œæ³¨è§†ç‚¹çš„ç›¸æœºå‡½æ•°ã€‚ç„¶åï¼Œæ­¤å‡½æ•°å°†è¿”å›ä¸€ä¸ª 3x3 å˜æ¢çŸ©é˜µï¼Œæˆ‘ä»¬å¯ä»¥å°†å…‰çº¿æ–¹å‘ä¹˜ä»¥è¯¥çŸ©é˜µã€‚

```cpp
mat3 camera(vec3 cameraPos, vec3 lookAtPoint) {
    vec3 cd = normalize(lookAtPoint - cameraPos); // camera direction
    vec3 cr = normalize(cross(vec3(0, 1, 0), cd)); // camera right
    vec3 cu = normalize(cross(cd, cr)); // camera up

    return mat3(-cr, cu, -cd);
}
```

è¦äº†è§£æˆ‘ä»¬æ˜¯å¦‚ä½•å¾—å‡ºè¿™ä¸ªçŸ©é˜µçš„ï¼Œè®©æˆ‘ä»¬çœ‹çœ‹ä¸‹å›¾ã€‚å®ƒæ˜¯åœ¨ç½‘ç«™ [**Learn OpenGL**](https://learnopengl.com/) ä¸Šåˆ›å»ºçš„ï¼Œè¯¥ç½‘ç«™æ˜¯å­¦ä¹  OpenGL å›¾å½¢ API çš„ç»ä½³èµ„æºã€‚

<p align="center">![-](./img/img-2.png)</p>
<p align="center">ç”± [**Learn OpenGL**](https://learnopengl.com/Getting-started/Camera) æä¾›çš„æ‘„åƒæœº/è§†å›¾ç©ºé—´ æä¾›</p>

ä¸Šå›¾ä¼ è¾¾äº†å¾ˆå¤šå…³äº `3x3` çŸ©é˜µæ˜¯å¦‚ä½•åˆ›å»ºçš„ã€‚æˆ‘ä»¬éœ€è¦é€šè¿‡åˆ†æä¸‰ä¸ªé‡è¦çš„æ‘„åƒæœºå‘é‡æ¥å¼„æ¸…æ¥šæ‘„åƒæœºçš„è§‚å¯Ÿä½ç½®ä»¥åŠå®ƒçš„å€¾æ–œæ–¹å¼ï¼š`Camera direction`å‘é‡ã€`Camera right`å‘é‡å’Œ `Camera up` å‘é‡ã€‚

åœ¨ç¬¬ `1` æ­¥ä¸­ï¼Œæˆ‘ä»¬ä»ç›¸æœºä½ç½®`cameraPos`å¼€å§‹ï¼Œè¯¥ä½ç½®ç­‰äºä»£ç ä¸­çš„å…‰çº¿åŸç‚¹ `ro`ã€‚

åœ¨ç¬¬ `2` æ­¥ä¸­ï¼Œæˆ‘ä»¬åˆ›å»ºä¸€ä¸ªç›¸å¯¹äº `lookat` ç‚¹çš„ç›¸æœºæ–¹å‘å‘é‡ã€‚åœ¨å›¾åƒä¸­ï¼Œè§‚å¯Ÿç‚¹ä½äº `3D` ç©ºé—´ä¸­çš„åŸç‚¹ï¼Œä½†æˆ‘ä»¬å¯ä»¥å°†æ­¤ç‚¹ç§»åŠ¨åˆ°ä»»ä½•æˆ‘ä»¬æƒ³è¦çš„ä»»ä½•ä½ç½®ã€‚è¯·æ³¨æ„æ‘„åƒæœºæ–¹å‘å¦‚ä½•æŒ‡å‘è¿œç¦»æ‘„åƒæœºçš„æ–¹å‘ã€‚è¿™æ„å‘³ç€å®ƒä½¿ç”¨äº†æˆ‘ä»¬åœ¨ ç¬¬ `6` éƒ¨åˆ† ä¸­äº†è§£çš„å³æ‰‹å®šåˆ™ã€‚

```cpp
vec3 cd = normalize(lookAtPoint - cameraPos); // camera direction
```

åœ¨æ­¥éª¤ `3` ä¸­ï¼Œæœ‰ä¸€ä¸ªç°è‰²çŸ¢é‡ä»æ‘„åƒæœºç›´æ¥æŒ‡å‘ä¸Šæ–¹ã€‚æ–¹å‘å‘é‡ `ï¼ˆ0, 1, 0)` è¡¨ç¤º `y` è½´çš„å•ä½å‘é‡ã€‚æˆ‘ä»¬é€šè¿‡å– `y` è½´çš„å•ä½å‘é‡å’Œç›¸æœºæ–¹å‘ä¹‹é—´çš„å‰ç§¯æ¥åˆ›å»ºâ€œç›¸æœºå³â€å‘é‡ã€‚è¿™å°†åˆ›å»ºæŒ‡å‘æ‘„åƒæœºå³ä¾§çš„çº¢è‰²çŸ¢é‡ã€‚

```cpp
normalize(cross(vec3(0, 1, 0), cd)); // camera right
```

åœ¨ç¬¬ `4` æ­¥ä¸­ï¼Œæˆ‘ä»¬é€šè¿‡å–ç›¸æœºæ–¹å‘å‘é‡å’Œ `camera right` å‘é‡ä¹‹é—´çš„å‰ç§¯æ¥æ‰¾åˆ° `camera up` å‘é‡ã€‚æ­¤â€œç›¸æœºå‘ä¸Šâ€çŸ¢é‡åœ¨å›¾åƒä¸­ç”±ä»ç›¸æœºä¼¸å‡ºçš„ç»¿è‰²çŸ¢é‡è¡¨ç¤ºã€‚

```cpp
vec3 cu = normalize(cross(cd, cr)); // camera up
```

æœ€åï¼Œæˆ‘ä»¬é€šè¿‡å°†è¿™äº›å‘é‡ç»„åˆåœ¨ä¸€èµ·æ¥åˆ›å»ºä¸€ä¸ªè½¬æ¢çŸ©é˜µï¼š

```cpp
mat3 camera(vec3 cameraPos, vec3 lookAtPoint) {
    vec3 cd = normalize(lookAtPoint - cameraPos); // camera direction
    vec3 cr = normalize(cross(vec3(0, 1, 0), cd)); // camera right
    vec3 cu = normalize(cross(cd, cr)); // camera up

    return mat3(-cr, cu, -cd); // negative signs can be turned positive (or vice versa) to flip coordinate space conventions
}
```

è®©æˆ‘ä»¬çœ‹çœ‹ `camera` å‡½æ•°çš„ `return` è¯­å¥ï¼š
```cpp
return mat3(-cr, cu, -cd);
```

æ­£è´Ÿä»ä½•è€Œæ¥ï¼Ÿç”±æˆ‘ä»¬æ¥å®šä¹‰ä¸€ä¸ªçº¦å®šï¼Œå³å¦‚ä½•æ ‡è®° 3D ç©ºé—´ä¸­æ¯ä¸ªè½´çš„æ­£æ–¹å‘æˆ–è´Ÿæ–¹å‘ã€‚è¿™æ˜¯æˆ‘å°†åœ¨æœ¬æ•™ç¨‹ä¸­ä½¿ç”¨çš„çº¦å®šã€‚æˆ‘ä»¬å¾ˆå¿«å°±ä¼šçœ‹åˆ°å½“æˆ‘ä»¬ç¿»è½¬æ–¹å‘æ—¶ä¼šå‘ç”Ÿä»€ä¹ˆã€‚

## åº”ç”¨ç›¸æœºçŸ©é˜µ

ç°åœ¨æˆ‘ä»¬å·²ç»åˆ›å»ºäº†ä¸€ä¸ª `camera` å‡½æ•°ï¼Œè®©æˆ‘ä»¬åœ¨ `mainImage` å‡½æ•°ä¸­ä½¿ç”¨å®ƒã€‚æˆ‘ä»¬å°†åˆ›å»ºä¸€ä¸ª `lookat` ç‚¹å¹¶å°†å…¶ä¼ é€’ç»™ `camera` å‡½æ•°ã€‚ç„¶åï¼Œæˆ‘ä»¬å°†å®ƒè¿”å›çš„çŸ©é˜µä¹˜ä»¥å…‰çº¿æ–¹å‘ï¼Œç±»ä¼¼äºæˆ‘ä»¬åœ¨[**ç¬¬ 9 éƒ¨åˆ†**](/category/9-æ‘„åƒæœºç§»åŠ¨)ä¸­æ‰€åšçš„ã€‚

```cpp
vec3 lp = vec3(0, 0, 0); // lookat point (aka camera target)
vec3 ro = vec3(0, 0, 3); // ray origin that represents camera position
vec3 rd = camera(ro, lp) * normalize(vec3(uv, -1)); // ray direction
```

å½“æ‚¨è¿è¡Œä»£ç æ—¶ï¼Œåœºæ™¯çœ‹èµ·æ¥åº”è¯¥å‡ ä¹ç›¸åŒã€‚ä½†æ˜¯ï¼Œæ‘„åƒæœºç°åœ¨ä»¥ 3D ç©ºé—´ä¸­çš„åŸç‚¹ä¸ºç›®æ ‡ã€‚ç”±äºç«‹æ–¹ä½“ç¦»åœ°é¢ 0.5 ä¸ªå•ä½ï¼Œå› æ­¤æ‘„åƒæœºä¼šä»ä¸­å¿ƒç•¥å¾®å€¾æ–œã€‚æˆ‘ä»¬å¯ä»¥é€šè¿‡æ›´æ”¹æ³¨è§†ç‚¹æ¥åŒ¹é…ç»¿è‰²ç«‹æ–¹ä½“çš„ä½ç½®ï¼Œä»è€Œå°†ç›¸æœºç›´æ¥å¯¹å‡†ç»¿è‰²ç«‹æ–¹ä½“çš„ä¸­å¿ƒã€‚

```cpp
vec3 lp = vec3(0, 0.5, -4);
```

å‡è®¾æˆ‘ä»¬ç°åœ¨æƒ³æŸ¥çœ‹çº¢è‰²ç«‹æ–¹ä½“ã€‚å®ƒç›®å‰åœ¨ `3D` ç©ºé—´ä¸­çš„ä½ç½®ä¸º `ï¼ˆ-4ï¼Œ 0.5ï¼Œ -4ï¼‰`ã€‚è®©æˆ‘ä»¬æ›´æ”¹ `lookat` ç‚¹ä»¥åŒ¹é…è¯¥ä½ç½®ã€‚

```cpp
vec3 lp = vec3(-4, 0.5, -4);
```

æ‚¨åº”è¯¥çœ‹åˆ°æ‘„åƒæœºç°åœ¨æŒ‡å‘çº¢è‰²ç«‹æ–¹ä½“ï¼Œå¹¶ä¸”å®ƒåº”è¯¥ä½äºç”»å¸ƒçš„ä¸­å¿ƒã€‚

<p align="center">![-](./img/img-3.png)</p>

ç°åœ¨è®©æˆ‘ä»¬çœ‹çœ‹è“è‰²ç«‹æ–¹ä½“ã€‚å®ƒåœ¨ `3D` ç©ºé—´ä¸­çš„ä½ç½®ä¸º `ï¼ˆ4ï¼Œ 0.5ï¼Œ -4ï¼‰`ï¼Œå› æ­¤æˆ‘ä»¬å°† `lookat point` æ›´æ”¹ä¸ºç­‰äºè¯¥å€¼ã€‚

```cpp
vec3 lp = vec3(4, 0.5, -4);
```

æ‚¨åº”è¯¥çœ‹åˆ°æ‘„åƒæœºç°åœ¨æŒ‡å‘è“è‰²ç«‹æ–¹ä½“ï¼Œå¹¶ä¸”å®ƒåº”è¯¥ä½äºç”»å¸ƒçš„ä¸­å¿ƒã€‚
<p align="center">![-](./img/img-4.png)</p>

æ‚¨å¯ä»¥åœ¨ä¸‹é¢æ‰¾åˆ°å®Œæˆçš„ä»£ç ï¼š

```cpp
// Constants
const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float PRECISION = 0.001;
const float EPSILON = 0.0005;
const float PI = 3.14159265359;

// Rotation matrix around the X axis.
mat3 rotateX(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(1, 0, 0),
        vec3(0, c, -s),
        vec3(0, s, c)
    );
}

// Rotation matrix around the Y axis.
mat3 rotateY(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, 0, s),
        vec3(0, 1, 0),
        vec3(-s, 0, c)
    );
}

// Rotation matrix around the Z axis.
mat3 rotateZ(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, -s, 0),
        vec3(s, c, 0),
        vec3(0, 0, 1)
    );
}

// Identity matrix.
mat3 identity() {
    return mat3(
        vec3(1, 0, 0),
        vec3(0, 1, 0),
        vec3(0, 0, 1)
    );
}

struct Surface {
    float sd; // signed distance value
    vec3 col; // color
};

Surface sdBox( vec3 p, vec3 b, vec3 offset, vec3 col, mat3 transform)
{
  p = (p - offset) * transform; // apply transformation matrix
  vec3 q = abs(p) - b;
  float d = length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
  return Surface(d, col);
}

Surface sdFloor(vec3 p, vec3 col) {
  float d = p.y + 1.;
  return Surface(d, col);
}

Surface minWithColor(Surface obj1, Surface obj2) {
  if (obj2.sd < obj1.sd) return obj2;
  return obj1;
}

Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0));
  Surface co = sdFloor(p, floorColor);
  co = minWithColor(co, sdBox(p, vec3(1), vec3(-4, 0.5, -4), vec3(1, 0, 0), identity())); // left cube
  co = minWithColor(co, sdBox(p, vec3(1), vec3(0, 0.5, -4), vec3(0, 0.65, 0.2), identity())); // center cube
  co = minWithColor(co, sdBox(p, vec3(1), vec3(4, 0.5, -4), vec3(0, 0.55, 2), identity())); // right cube
  return co;
}

Surface rayMarch(vec3 ro, vec3 rd, float start, float end) {
  float depth = start;
  Surface co; // closest object

  for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
    vec3 p = ro + depth * rd;
    co = sdScene(p);
    depth += co.sd;
    if (co.sd < PRECISION || depth > end) break;
  }

  co.sd = depth;

  return co;
}

vec3 calcNormal(in vec3 p) {
    vec2 e = vec2(1, -1) * EPSILON;
    return normalize(
      e.xyy * sdScene(p + e.xyy).sd +
      e.yyx * sdScene(p + e.yyx).sd +
      e.yxy * sdScene(p + e.yxy).sd +
      e.xxx * sdScene(p + e.xxx).sd);
}

mat3 camera(vec3 cameraPos, vec3 lookAtPoint) {
    vec3 cd = normalize(lookAtPoint - cameraPos); // camera direction
    vec3 cr = normalize(cross(vec3(0, 1, 0), cd)); // camera right
    vec3 cu = normalize(cross(cd, cr)); // camera up

    return mat3(-cr, cu, -cd);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
  vec3 backgroundColor = vec3(0.835, 1, 1);

  vec3 col = vec3(0);
  vec3 lp = vec3(4, 0.5, -4); // lookat point (aka camera target)
  vec3 ro = vec3(0, 0, 3); // ray origin that represents camera position
  vec3 rd = camera(ro, lp) * normalize(vec3(uv, -1)); // ray direction

  Surface co = rayMarch(ro, rd, MIN_DIST, MAX_DIST); // closest object

  if (co.sd > MAX_DIST) {
    col = backgroundColor; // ray didn't hit anything
  } else {
    vec3 p = ro + rd * co.sd; // point on cube or floor we discovered from ray marching
    vec3 normal = calcNormal(p);
    vec3 lightPosition = vec3(2, 2, 7);
    vec3 lightDirection = normalize(lightPosition - p);

    float dif = clamp(dot(normal, lightDirection), 0.3, 1.); // diffuse reflection

    col = dif * co.col + backgroundColor * .2; // Add a bit of background color to the diffuse color
  }

  // Output to screen
  fragColor = vec4(col, 1.0);
}
```

## è°ƒæ•´æ­£è´Ÿæ–¹å‘çš„çº¦å®š
ä¹‹å‰ï¼Œæˆ‘ä»¬çœ‹åˆ° camera å‡½æ•°è¿”å›ä¸€ä¸ªç”±ä¸‰ä¸ªç›¸æœºå‘é‡ç»„æˆçš„çŸ©é˜µã€‚

```cpp
mat3 camera(vec3 cameraPos, vec3 lookAtPoint) {
    vec3 cd = normalize(lookAtPoint - cameraPos); // camera direction
    vec3 cr = normalize(cross(vec3(0, 1, 0), cd)); // camera right
    vec3 cu = normalize(cross(cd, cr)); // camera up

    return mat3(-cr, cu, -cd);
}
```

å¦‚æœæˆ‘ä»¬è®¾ç½® `Lookat` ç‚¹ä»¥å°†ç›¸æœºå¯¹å‡†ç»¿è‰²ç«‹æ–¹ä½“ï¼Œåˆ™æœ‰ä»¥ä¸‹ä»£ç ï¼š

```cpp
vec3 lp = vec3(0, 0.5, -4); // lookat point (aka camera target)
vec3 ro = vec3(0, 0, 3); // ray origin that represents camera position
vec3 rd = camera(ro, lp) * normalize(vec3(uv, -1)); // ray direction
```

è¿™å°†ç”Ÿæˆæœ¬æ•™ç¨‹å¼€å¤´çš„åœºæ™¯ï¼Œå…¶ä¸­çº¢è‰²ç«‹æ–¹ä½“ä½äºç»¿è‰²ç«‹æ–¹ä½“çš„å·¦ä¾§ï¼Œè“è‰²ç«‹æ–¹ä½“ä½äºç»¿è‰²ç«‹æ–¹ä½“çš„å³ä¾§ã€‚

<p align="center">![-](./img/img-4.png)</p>

å¦‚æœæˆ‘ä»¬å†³å®šåœ¨ camera å‡½æ•°ä¸­ä½¿ç”¨æ­£çš„ cr å€¼ï¼Œé‚£ä¹ˆè®©æˆ‘ä»¬çœ‹çœ‹ä¼šå‘ç”Ÿä»€ä¹ˆã€‚

<p align="center">![-](./img/img-5.png)</p>

çº¢è‰²ç«‹æ–¹ä½“å’Œè“è‰²ç«‹æ–¹ä½“ä¼¼ä¹äº¤æ¢äº†ä½ç½®ï¼Œä½†è¦æ³¨æ„åœ°ç –ã€‚ä»–ä»¬ä¹Ÿè¢«è°ƒæ¢äº†ã€‚`æ‘„åƒæœºå³ä¾§`çŸ¢é‡æ˜¯ç›¸åçš„ï¼Œè¿™ä¼šå¯¼è‡´æ•´ä¸ªåœºæ™¯ç¿»è½¬ï¼Œå°±åƒæŸ¥çœ‹åŸå§‹åœºæ™¯çš„é•œåƒä¸€æ ·ã€‚
ä½¿ç”¨æ­£ `cr` ä¼šå½±å“ç›¸æœºçœ‹åˆ°çš„å†…å®¹ï¼Œä¹Ÿä¼šä½¿æˆ‘ä»¬çš„ç«‹æ–¹ä½“çš„ä½ç½®çœ‹èµ·æ¥ä»¤äººå›°æƒ‘ã€‚æˆ‘ä»¬çš„ `x` è½´è®¾è®¡ä¸ºç”»å¸ƒä¸­å¿ƒçš„å·¦ä¾§ä¸ºè´Ÿå€¼ï¼Œä¸­å¿ƒå³ä¾§çš„æ­£å€¼ã€‚ç¿»è½¬ `cr` ä¹Ÿæ„å‘³ç€ç¿»è½¬é‚£ä¸ªçº¦å®šã€‚

å¦‚æœæˆ‘ä»¬å°†ç›¸æœºæ–¹å‘çš„å€¼ `cd` åè½¬ä¸ºæ­£å€¼è€Œä¸æ˜¯è´Ÿå€¼ï¼Œå®ƒä¼šè½¬åŠ¨ç›¸æœºï¼Œå› ä¸ºå®ƒä¼šç¿»è½¬æˆ‘ä»¬çš„ `z` è½´çº¦å®šã€‚

ç¿»è½¬ `z` è½´çº¦å®šçš„å¦ä¸€ç§æ–¹æ³•æ˜¯å¯¹å…‰çº¿æ–¹å‘çš„ `z` åˆ†é‡ä½¿ç”¨æ­£å€¼ã€‚

```cpp
vec3 rd = normalize(vec3(uv, 1)); // positive one is being used instead of negative one
```

å½“æ‚¨å°†è¿™ç§æ›¿ä»£ç›¸æœºæ¨¡å‹ä¸æ³¨è§†ç‚¹ä¸€èµ·ä½¿ç”¨æ—¶ï¼Œæœ€å¥½äº†è§£æ‚¨ä¸ºæ¯ä¸ªè½´ä¸Šçš„æ­£æˆ–è´Ÿè®¾ç½®çš„çº¦å®šã€‚

ä½ å¯ä»¥å°è¯•ä½¿ç”¨ `cr`ã€`cu` å’Œ `cd` æ¥åˆ¶ä½œä¸€äº›æœ‰è¶£çš„æ•ˆæœã€‚ç¡®ä¿å°†å…‰çº¿æ–¹å‘ `rd`ï¼Œæ”¹å›ä½¿ç”¨è´Ÿ `1`ã€‚

ä»¥ä¸‹ä»£ç å¯ä»¥åœ¨ `z` è½´ä¸Šåˆ›å»º `slingshot` æ•ˆæœï¼Œä½¿å…¶çœ‹èµ·æ¥åƒæ˜¯æ‘„åƒæœºéå¸¸å¿«é€Ÿåœ°ç¼©å°å’Œæ”¾å¤§ã€‚ä¹Ÿè®¸è¿™å¯ä»¥ç”¨æ¥åˆ›é€  `warp drive` æ•ˆæœï¼ŸğŸ¤”

```cpp
mat3 camera(vec3 cameraPos, vec3 lookAtPoint) {
    vec3 cd = normalize(lookAtPoint - cameraPos); // camera direction
    vec3 cr = normalize(cross(vec3(0, 1, 0), cd)); // camera right
    vec3 cu = normalize(cross(cd, cr)); // camera up

    return mat3(-cr, cu, abs(cos(iTime)) * -cd);
}
```

<p align="center">![-](./img/gif-1.gif)</p>

ç»§ç»­å°†ç›¸æœºçŸ©é˜µæ›´æ”¹å›æ­£å¸¸ï¼Œç„¶åå†ç»§ç»­æœ¬æ•™ç¨‹çš„ä¸‹ä¸€éƒ¨åˆ†ã€‚
```cpp
mat3 camera(vec3 cameraPos, vec3 lookAtPoint) {
    vec3 cd = normalize(lookAtPoint - cameraPos); // camera direction
    vec3 cr = normalize(cross(vec3(0, 1, 0), cd)); // camera right
    vec3 cu = normalize(cross(cd, cr)); // camera up

    return mat3(-cr, cu, -cd);
}
```
