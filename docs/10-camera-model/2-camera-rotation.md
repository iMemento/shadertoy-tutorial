---
sidebar_position: 2
---

# 10.2 å›´ç»•ç›®æ ‡æ—‹è½¬æ‘„åƒæœº

## å›´ç»•ç›®æ ‡æ—‹è½¬

å‡è®¾æˆ‘ä»¬æƒ³åœ¨åœºæ™¯å‘¨å›´ä»¥åœ†å½¢è·¯å¾„æ—‹è½¬æ‘„åƒæœºï¼ŒåŒæ—¶ä¿æŒæ‘„åƒæœºå¯¹å‡†ç»¿è‰²ç«‹æ–¹ä½“ã€‚æˆ‘ä»¬å°†æ‘„åƒæœºä¿æŒåœ¨ç¦»åœ°é¢çš„æ’å®šé«˜åº¦`y åˆ†é‡`ã€‚ç”±äºæ‰€æœ‰ä¸‰ä¸ªç«‹æ–¹ä½“çš„ä½ç½® `y åˆ†é‡`å‡ä¸º `0.5`ï¼Œå› æ­¤æˆ‘ä»¬å°†ç¡®ä¿ `ro` çš„ `y` åˆ†é‡ï¼Œå³å…‰çº¿åŸç‚¹ï¼ˆç›¸æœºä½ç½®ï¼‰ä¹Ÿç­‰äº `0.5`ã€‚

å¦‚æœæˆ‘ä»¬æƒ³è®©ç›¸æœºæ²¿ç€å›´ç»•ç«‹æ–¹ä½“å¤§å°çš„åœ†å½¢è·¯å¾„ç§»åŠ¨ï¼Œé‚£ä¹ˆæˆ‘ä»¬åº”è¯¥ä¸“æ³¨äºæ”¹å˜å…‰çº¿åŸç‚¹ `ro` çš„ `x` åˆ†é‡å’Œ `z` åˆ†é‡ã€‚

å¦‚æœæˆ‘ä»¬ä»è‡ªä¸Šè€Œä¸‹çš„è§†è§’æŸ¥çœ‹`cube`ï¼Œåˆ™ä¼šçœ‹åˆ°ç±»ä¼¼äºä¸‹å›¾çš„è§†å›¾ã€‚

<p align="center">![-](./img/img-6.png)</p>

åœ¨ä¸Šå›¾ä¸­ï¼Œæ‘„åƒæœºå°†éµå¾ªåœ†å½¢è·¯å¾„ï¼ˆé»‘è‰²ï¼‰ã€‚ä»è‡ªä¸Šè€Œä¸‹çš„è§†è§’æ¥çœ‹ï¼Œåœºæ™¯æ˜¾ç¤ºä¸º `2D`ï¼Œåªæœ‰ä¸€ä¸ª `x` è½´ï¼ˆçº¢è‰²ï¼‰å’Œ `z` è½´ï¼ˆè“è‰²ï¼‰ã€‚

è¿™ä¸ªæƒ³æ³•æ˜¯æ”¹å˜ `ro` çš„ `x` åˆ†é‡å’Œ `z` åˆ†é‡å€¼ï¼Œä½¿å…¶éµå¾ªå¾ªç¯è·¯å¾„ã€‚æˆ‘ä»¬å¯ä»¥é€šè¿‡å°† `ro.x` å’Œ `ro.z` è½¬æ¢ä¸º[**æåæ ‡**](https://mathworld.wolfram.com/PolarCoordinates.html)æ¥å®ç°è¿™ä¸€ç‚¹ã€‚

```cpp
vec3 ro = vec3(0, 0.5, 0);
ro.x = cameraRadius * cos(theta);
ro.z = cameraRadius * sin(theta);
```

`Camera radius` ï¼ˆæ‘„åƒæœºåŠå¾„ï¼‰ çš„å€¼å°†å¢åŠ ï¼Œç›´åˆ°æˆ‘ä»¬å¯ä»¥çœ‹åˆ°åœºæ™¯ä¸­çš„æ‰€æœ‰ç«‹æ–¹ä½“ã€‚æˆ‘ä»¬ç›®å‰åœ¨ `3D` ç©ºé—´ä¸­çš„ä»¥ä¸‹ä½ç½®æœ‰ä¸‰ä¸ªç«‹æ–¹ä½“ï¼ˆåœ¨ `sdScene` å‡½æ•°ä¸­å®šä¹‰ï¼‰ï¼š

```cpp
vec3(-4, 0.5, -4) // left cube
vec3(0, 0.5, -4) // center cube
vec3(4, 0.5, -4) // right cube
```

å› æ­¤ï¼Œå°†åŠå¾„è®¾ç½®ä¸ºç±»ä¼¼äº `10` å¯èƒ½æ˜¯å®‰å…¨çš„ï¼Œå› ä¸ºå·¦ç«‹æ–¹ä½“å’Œå³ç«‹æ–¹ä½“ä¹‹é—´çš„è·ç¦»ä¸º `4 - ï¼ˆ-4ï¼‰ = 8` ä¸ªå•ä½ã€‚

åœ¨æˆ‘ä»¬çš„ä»£ç ä¸­ï¼Œæˆ‘ä»¬å°†å°„çº¿åŸç‚¹çš„ `x` åˆ†é‡å’Œ `z` åˆ†é‡è½¬æ¢ä¸ºåŠå¾„ä¸º `10` çš„æåæ ‡ã€‚ç„¶åï¼Œæˆ‘ä»¬è¿˜å°†åœ†å½¢è·¯å¾„åç§»ä¸€ä¸ªåç§»é‡ï¼Œä½¿è§‚å¯Ÿç‚¹æˆä¸ºåœ†å½¢è·¯å¾„å½¢æˆçš„åœ†å¿ƒã€‚

```cpp
vec3 lp = vec3(0, 0.5, -4); // lookat point (aka camera target)
vec3 ro = vec3(0, 0.5, 0); // ray origin that represents camera position

float cameraRadius = 10.;
ro.x = cameraRadius * cos(iTime) + lp.x; // convert x-component to polar and add offset
ro.z = cameraRadius * sin(iTime) + lp.z; // convert z-component to polar and add offset

vec3 rd = camera(ro, lp) * normalize(vec3(uv, -1)); // ray direction
```

å½“æ‚¨è¿è¡Œä»£ç æ—¶ï¼Œæ‚¨åº”è¯¥ä¼šçœ‹åˆ°æ‘„åƒæœºåœ¨åœºæ™¯ä¸­æ—‹è½¬ï¼Œå› ä¸ºå®ƒéµå¾ªåœ†å½¢è·¯å¾„ï¼Œä½†å®ƒä»ç„¶ä½¿ç”¨æˆ‘ä»¬çš„è§‚å¯Ÿç‚¹çœ‹ç€ç»¿è‰²ç«‹æ–¹ä½“ã€‚

<p align="center">![-](./img/gif-2.gif)</p>

ä»è‡ªä¸Šè€Œä¸‹çš„è§†è§’æ¥çœ‹ï¼Œæˆ‘ä»¬çš„ç›¸æœºåœ¨ä¸€ä¸ªåœ†åœˆä¸­ç§»åŠ¨ï¼Œè¯¥åœ†åœˆè¢«è§‚å¯Ÿç‚¹çš„ `x` åˆ†é‡å’Œ `z` åˆ†é‡åç§»ï¼Œå› æ­¤æˆ‘ä»¬å¯ä»¥ç¡®ä¿è§‚å¯Ÿç‚¹ä¿æŒåœ¨åœ†çš„ä¸­å¿ƒã€‚è¿™å¯ç¡®ä¿åœ¨æ•´ä¸ªæ—‹è½¬è¿‡ç¨‹ä¸­ï¼Œä¸ç»¿è‰²ç«‹æ–¹ä½“çš„è·ç¦»ï¼ˆåœ†çš„åŠå¾„ï¼‰ä¸ç»¿è‰²ç«‹æ–¹ä½“ä¿æŒç›¸ç­‰çš„è·ç¦»ã€‚

<p align="center">![-](./img/gif-3.gif)</p>

æ‚¨å¯ä»¥ä½¿ç”¨æˆ‘åœ¨ [**Desmos**](https://www.desmos.com/calculator/5emxoibru1) ä¸Šåˆ›å»ºçš„å›¾è¡¨æ¥è¯•éªŒå¾ªç¯è·¯å¾„ã€‚æƒ³è±¡ä¸€ä¸‹ç»¿è‰²çš„ç«‹æ–¹ä½“ä½äºåœ†åœˆçš„ä¸­å¿ƒã€‚

ä½¿ç”¨è§‚å¯Ÿç‚¹ä½¿æˆ‘ä»¬çš„ç›¸æœºæ›´åŠ çµæ´»ã€‚æˆ‘ä»¬å¯ä»¥æ²¿ `y` è½´å°†ç›¸æœºå‡é«˜å¾—æ›´é«˜ï¼Œç„¶åå†æ¬¡ç»•åœˆæ—‹è½¬ï¼Œä½†å¯ä»¥é¸Ÿç°ç«‹æ–¹ä½“ã€‚

è®©æˆ‘ä»¬å°è¯•è°ƒæ•´æ‘„åƒæœºçš„é«˜åº¦ ï¼ˆå…‰çº¿åŸç‚¹ï¼‰ï¼Œçœ‹çœ‹ä¼šå‘ç”Ÿä»€ä¹ˆã€‚

```cpp
vec3 ro = vec3(0, 5, 0);
```

å½“æˆ‘ä»¬è¿è¡Œä»£ç æ—¶ï¼Œæˆ‘ä»¬åº”è¯¥çœ‹åˆ°ç›¸æœºç°åœ¨å›´ç»•ä¸‰ä¸ªç«‹æ–¹ä½“æ—‹è½¬ï¼Œä½†å®ƒçš„ä½ç½®æ›´é«˜ã€‚è¿™å°±åƒæˆ‘ä»¬æ˜¯ä¸€åä¹˜åç›´å‡æœºå››å¤„é£è¡Œçš„æ–°é—»è®°è€…ã€‚
<p align="center">![-](./img/gif-4.gif)</p>

å¦‚æœæ‚¨æ›´æ”¹äº†è§‚å¯Ÿç‚¹ï¼Œåˆ™åº”å¼€å§‹å›´ç»•è¯¥æ–°ç‚¹æ—‹è½¬ï¼

æ‚¨å¯ä»¥åœ¨ä¸‹é¢æ‰¾åˆ°å®Œæˆçš„ä»£ç ï¼š

```cpp
// Constants
const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float PRECISION = 0.001;
const float EPSILON = 0.0005;
const float PI = 3.14159265359;

// Rotation matrix around the X axis.
mat3 rotateX(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(1, 0, 0),
        vec3(0, c, -s),
        vec3(0, s, c)
    );
}

// Rotation matrix around the Y axis.
mat3 rotateY(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, 0, s),
        vec3(0, 1, 0),
        vec3(-s, 0, c)
    );
}

// Rotation matrix around the Z axis.
mat3 rotateZ(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, -s, 0),
        vec3(s, c, 0),
        vec3(0, 0, 1)
    );
}

// Identity matrix.
mat3 identity() {
    return mat3(
        vec3(1, 0, 0),
        vec3(0, 1, 0),
        vec3(0, 0, 1)
    );
}

struct Surface {
    float sd; // signed distance value
    vec3 col; // color
};

Surface sdBox( vec3 p, vec3 b, vec3 offset, vec3 col, mat3 transform)
{
  p = (p - offset) * transform; // apply transformation matrix
  vec3 q = abs(p) - b;
  float d = length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
  return Surface(d, col);
}

Surface sdFloor(vec3 p, vec3 col) {
  float d = p.y + 1.;
  return Surface(d, col);
}

Surface minWithColor(Surface obj1, Surface obj2) {
  if (obj2.sd < obj1.sd) return obj2;
  return obj1;
}

Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0));
  Surface co = sdFloor(p, floorColor);
  co = minWithColor(co, sdBox(p, vec3(1), vec3(-4, 0.5, -4), vec3(1, 0, 0), identity())); // left cube
  co = minWithColor(co, sdBox(p, vec3(1), vec3(0, 0.5, -4), vec3(0, 0.65, 0.2), identity())); // center cube
  co = minWithColor(co, sdBox(p, vec3(1), vec3(4, 0.5, -4), vec3(0, 0.55, 2), identity())); // right cube
  return co;
}

Surface rayMarch(vec3 ro, vec3 rd, float start, float end) {
  float depth = start;
  Surface co; // closest object

  for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
    vec3 p = ro + depth * rd;
    co = sdScene(p);
    depth += co.sd;
    if (co.sd < PRECISION || depth > end) break;
  }

  co.sd = depth;

  return co;
}

vec3 calcNormal(in vec3 p) {
    vec2 e = vec2(1, -1) * EPSILON;
    return normalize(
      e.xyy * sdScene(p + e.xyy).sd +
      e.yyx * sdScene(p + e.yyx).sd +
      e.yxy * sdScene(p + e.yxy).sd +
      e.xxx * sdScene(p + e.xxx).sd);
}

mat3 camera(vec3 cameraPos, vec3 lookAtPoint) {
    vec3 cd = normalize(lookAtPoint - cameraPos); // camera direction
    vec3 cr = normalize(cross(vec3(0, 1, 0), cd)); // camera right
    vec3 cu = normalize(cross(cd, cr)); // camera up

    return mat3(-cr, cu, -cd);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
  vec3 backgroundColor = vec3(0.835, 1, 1);

  vec3 col = vec3(0);
  vec3 lp = vec3(0, 0.5, -4); // lookat point (aka camera target)
  vec3 ro = vec3(0, 5, 0); // ray origin that represents camera position

  float cameraRadius = 10.;
  ro.x = cameraRadius * cos(iTime) + lp.x; // convert to polar
  ro.z = cameraRadius * sin(iTime) + lp.z;

  vec3 rd = camera(ro, lp) * normalize(vec3(uv, -1)); // ray direction

  Surface co = rayMarch(ro, rd, MIN_DIST, MAX_DIST); // closest object

  if (co.sd > MAX_DIST) {
    col = backgroundColor; // ray didn't hit anything
  } else {
    vec3 p = ro + rd * co.sd; // point on cube or floor we discovered from ray marching
    vec3 normal = calcNormal(p);
    vec3 lightPosition = vec3(2, 2, 7);
    vec3 lightDirection = normalize(lightPosition - p);

    float dif = clamp(dot(normal, lightDirection), 0.3, 1.); // diffuse reflection

    col = dif * co.col + backgroundColor * .2; // Add a bit of background color to the diffuse color
  }

  // Output to screen
  fragColor = vec4(col, 1.0);
}
```

## ä½¿ç”¨é¼ æ ‡æ—‹è½¬æ‘„åƒæœº
æ‚¨ä¹Ÿå¯ä»¥ä½¿ç”¨é¼ æ ‡åœ¨åœºæ™¯ä¸­ç§»åŠ¨æ‘„åƒæœºï¼Œä½†è¿™éœ€è¦ä¸€äº›é¢å¤–çš„è®¾ç½®ã€‚æ­£å¦‚æˆ‘ä»¬åœ¨æœ¬ç³»åˆ—æ•™ç¨‹çš„[**ç¬¬ 9 éƒ¨åˆ†**](/category/9-æ‘„åƒæœºç§»åŠ¨)ä¸­å­¦åˆ°çš„é‚£æ ·ï¼ŒiMouse å…¨å±€å˜é‡æä¾›é¼ æ ‡ä½ç½®æ•°æ®ã€‚

æˆ‘ä»¬å¯ä»¥ä½¿ç”¨ä»¥ä¸‹è¡Œåˆ›å»º `mouse UV` åæ ‡ï¼š

```cpp
vec2 mouseUV = iMouse.xy/iResolution.xy; // Range: <0, 1>
```

æˆ‘ä»¬å°†æ›¿æ¢ä»¥ä¸‹ä¸‰è¡Œï¼Œå› ä¸ºæˆ‘ä»¬ä½¿ç”¨çš„æ˜¯é¼ æ ‡åœ¨åœºæ™¯ä¸­æ—‹è½¬ï¼Œè€Œä¸æ˜¯ä½¿ç”¨æ—¶é—´ã€‚

```cpp
float cameraRadius = 10.;
ro.x = cameraRadius * cos(iTime) + lp.x; // convert to polar
ro.z = cameraRadius * sin(iTime) + lp.z;
```

ä»¥ä¸‹ä»£ç å°†æ›¿æ¢ä¸Šè¿°ä»£ç ï¼š

```cpp
float cameraRadius = 2.;
ro.yz = ro.yz * cameraRadius * rotate2d(mix(PI/2., 0., mouseUV.y));
ro.xz = ro.xz * rotate2d(mix(-PI, PI, mouseUV.x)) + vec2(lp.x, lp.z); // remap mouseUV.x to <-pi, pi> range
```

åŒæ ·ï¼Œæˆ‘ä»¬ä½¿ç”¨ `mix å‡½æ•°`é‡æ–°æ˜ å°„é¼ æ ‡ä½ç½®çš„ `x` åˆ†é‡ã€‚è¿™ä¸€æ¬¡ï¼Œæˆ‘ä»¬å°†å€¼ä» `<0,1>` èŒƒå›´é‡æ–°æ˜ å°„åˆ° `<-Ï€ï¼Œ Ï€>` èŒƒå›´ã€‚æˆ‘ä»¬è¿˜éœ€è¦æ·»åŠ  `lookat point` çš„ `x-component` å’Œ `z-component`ã€‚

è¯·æ³¨æ„ï¼Œæˆ‘ä»¬æœ‰ä¸€ä¸ªæœªæŒ‡å®šè½´çš„ `rotate2d å‡½æ•°`ã€‚æ­¤å‡½æ•°å°†ä½¿ç”¨ `2D` çŸ©é˜µæä¾› `2D` æ—‹è½¬ã€‚åœ¨ä»£ç é¡¶éƒ¨æ·»åŠ ä»¥ä¸‹å‡½æ•°ã€‚

```cpp
mat2 rotate2d(float theta) {
  float s = sin(theta), c = cos(theta);
  return mat2(c, -s, s, c);
}
```

å’Œä»¥å‰ä¸€æ ·ï¼Œä½ å¯èƒ½éœ€è¦ä½¿ç”¨ `cameraRadius`ï¼Œç›´åˆ°å®ƒçœ‹èµ·æ¥ä¸é”™ã€‚æ‚¨å®Œæˆçš„ä»£ç åº”å¦‚ä¸‹æ‰€ç¤ºï¼š

```cpp
// Constants
const int MAX_MARCHING_STEPS = 255;
const float MIN_DIST = 0.0;
const float MAX_DIST = 100.0;
const float PRECISION = 0.001;
const float EPSILON = 0.0005;
const float PI = 3.14159265359;

// Rotate around a circular path
mat2 rotate2d(float theta) {
  float s = sin(theta), c = cos(theta);
  return mat2(c, -s, s, c);
}

// Rotation matrix around the X axis.
mat3 rotateX(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(1, 0, 0),
        vec3(0, c, -s),
        vec3(0, s, c)
    );
}

// Rotation matrix around the Y axis.
mat3 rotateY(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, 0, s),
        vec3(0, 1, 0),
        vec3(-s, 0, c)
    );
}

// Rotation matrix around the Z axis.
mat3 rotateZ(float theta) {
    float c = cos(theta);
    float s = sin(theta);
    return mat3(
        vec3(c, -s, 0),
        vec3(s, c, 0),
        vec3(0, 0, 1)
    );
}

// Identity matrix.
mat3 identity() {
    return mat3(
        vec3(1, 0, 0),
        vec3(0, 1, 0),
        vec3(0, 0, 1)
    );
}

struct Surface {
    float sd; // signed distance value
    vec3 col; // color
};

Surface sdBox( vec3 p, vec3 b, vec3 offset, vec3 col, mat3 transform)
{
  p = (p - offset) * transform; // apply transformation matrix
  vec3 q = abs(p) - b;
  float d = length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
  return Surface(d, col);
}

Surface sdFloor(vec3 p, vec3 col) {
  float d = p.y + 1.;
  return Surface(d, col);
}

Surface minWithColor(Surface obj1, Surface obj2) {
  if (obj2.sd < obj1.sd) return obj2;
  return obj1;
}

Surface sdScene(vec3 p) {
  vec3 floorColor = vec3(1. + 0.7*mod(floor(p.x) + floor(p.z), 2.0));
  Surface co = sdFloor(p, floorColor);
  co = minWithColor(co, sdBox(p, vec3(1), vec3(-4, 0.5, -4), vec3(1, 0, 0), identity())); // left cube
  co = minWithColor(co, sdBox(p, vec3(1), vec3(0, 0.5, -4), vec3(0, 0.65, 0.2), identity())); // center cube
  co = minWithColor(co, sdBox(p, vec3(1), vec3(4, 0.5, -4), vec3(0, 0.55, 2), identity())); // right cube
  return co;
}

Surface rayMarch(vec3 ro, vec3 rd, float start, float end) {
  float depth = start;
  Surface co; // closest object

  for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
    vec3 p = ro + depth * rd;
    co = sdScene(p);
    depth += co.sd;
    if (co.sd < PRECISION || depth > end) break;
  }

  co.sd = depth;

  return co;
}

vec3 calcNormal(in vec3 p) {
    vec2 e = vec2(1, -1) * EPSILON;
    return normalize(
      e.xyy * sdScene(p + e.xyy).sd +
      e.yyx * sdScene(p + e.yyx).sd +
      e.yxy * sdScene(p + e.yxy).sd +
      e.xxx * sdScene(p + e.xxx).sd);
}

mat3 camera(vec3 cameraPos, vec3 lookAtPoint) {
    vec3 cd = normalize(lookAtPoint - cameraPos); // camera direction
    vec3 cr = normalize(cross(vec3(0, 1, 0), cd)); // camera right
    vec3 cu = normalize(cross(cd, cr)); // camera up

    return mat3(-cr, cu, -cd);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
  vec2 uv = (fragCoord-.5*iResolution.xy)/iResolution.y;
  vec2 mouseUV = iMouse.xy/iResolution.xy; // Range: <0, 1>
  vec3 backgroundColor = vec3(0.835, 1, 1);

  vec3 col = vec3(0);
  vec3 lp = vec3(0, 0.5, -4); // lookat point (aka camera target)
  vec3 ro = vec3(0, 5, 0); // ray origin that represents camera position

  float cameraRadius = 2.;
  ro.yz = ro.yz * cameraRadius * rotate2d(mix(PI/2., 0., mouseUV.y));
  ro.xz = ro.xz * rotate2d(mix(-PI, PI, mouseUV.x)) + vec2(lp.x, lp.z);

  vec3 rd = camera(ro, lp) * normalize(vec3(uv, -1)); // ray direction

  Surface co = rayMarch(ro, rd, MIN_DIST, MAX_DIST); // closest object

  if (co.sd > MAX_DIST) {
    col = backgroundColor; // ray didn't hit anything
  } else {
    vec3 p = ro + rd * co.sd; // point on cube or floor we discovered from ray marching
    vec3 normal = calcNormal(p);
    vec3 lightPosition = vec3(2, 2, 7);
    vec3 lightDirection = normalize(lightPosition - p);

    float dif = clamp(dot(normal, lightDirection), 0.3, 1.); // diffuse reflection

    col = dif * co.col + backgroundColor * .2; // Add a bit of background color to the diffuse color
  }

  // Output to screen
  fragColor = vec4(col, 1.0);
}
```

ç°åœ¨ï¼Œæ‚¨å¯ä»¥ä½¿ç”¨é¼ æ ‡åœ¨åœºæ™¯ä¸­æ—‹è½¬ï¼ğŸ‰ æ›´å…·ä½“åœ°è¯´ï¼Œæ‚¨å¯ä»¥ä½¿ç”¨é¼ æ ‡å›´ç»•è§‚å¯Ÿç‚¹æ—‹è½¬ã€‚

## ç»“è®º
æˆ‘å¸Œæœ›æ‚¨ç°åœ¨çœ‹åˆ°è¿™ç§æ›¿ä»£ç›¸æœºå‹å·çš„å¼ºå¤§åŠŸèƒ½ï¼æ³¨è§†ç‚¹å¯ä»¥æ›´è½»æ¾åœ°åœ¨åœºæ™¯ä¸­ç§»åŠ¨æ‘„åƒæœºï¼ŒåŒæ—¶èšç„¦åœ¨å•ä¸ªç›®æ ‡ä¸Šã€‚